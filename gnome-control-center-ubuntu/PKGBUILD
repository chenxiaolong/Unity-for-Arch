# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-control-center-ubuntu
_ppa_rel=0ubuntu1~raring6
_translations=20130418
pkgver=3.8.2
pkgrel=103
pkgdesc="The Control Center for GNOME with Ubuntu's patches"
arch=('i686' 'x86_64')
groups=('gnome')
url="http://www.gnome.org"
license=('GPL')
depends=('accountsservice' 'cups-pk-helper' 'gnome-bluetooth' 'gnome-desktop' 'gnome-menus' 'gnome-online-accounts' 'gnome-settings-daemon-ubuntu' 'gsettings-desktop-schemas' 'gtk3-ubuntu' 'libgtop' 'network-manager-applet' 'sound-theme-freedesktop' 'upower' 'libpwquality' 'gnome-color-manager' 'smbclient' 'modemmanager' 'archlinux-artwork' 'webkitgtk3' 'system-config-printer')
makedepends=('gnome-common' 'gnome-doc-utils' 'intltool' 'docbook-xsl' 'patchutils' 'vala' 'desktop-file-utils')
optdepends=('gnome-user-share: Bluetooth and WebDAV file sharing'
            'rygel: media sharing'
            'vino: screen sharing'
            'openssh: remote login'
            'credentials-preferences-ubuntu: Ubuntu Online Accounts')
provides=("gnome-control-center=${pkgver}" 'FEATURE-libgnome-control-center')
conflicts=('gnome-control-center')
options=('!libtool' '!emptydirs')
install=gnome-control-center.install
source=("http://download.gnome.org/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/g/gnome-control-center/gnome-control-center_${pkgver}-${_ppa_rel}.debian.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-gnome-control-center.tar.gz"
        '0001_Rename_back_to_System_Settings.patch')
sha512sums=('3d3199969205c62a8e6039053675965a7d2b7069cd0ddb26b5568e652227084801c024c2d9c4da80e3f733419ff5159e3e360495355be13d3348755a50deedca'
            '4a1451572010da3e40cacda0948314913c2230b9e179d7595486b0a42db522601a40ae6f1b83976104522d800fa6081f694575aa75671e0c3b1c8e248b35ead9'
            'f08d00d02170d55b9908ae8aef2a13f9cbd393168bc70b28e31e8f35ab690903992ba380afbf7ef935d13ce81d3c3bec9b9db2711e2bbc7d1a245e700df5bdbc'
            '6a3e327f7a396e500ab5fda610fea2b1495b134033f57363701c4b9d7da37f1a044f632fd78c49a1dc8e7cd9f16b688c2f8726bed76f86a5d76564915a40d898')

prepare() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  # Apply Ubuntu's patches
    # Disable patches
      # Ubuntu is too lazy to port their patches to the latest version of IBus
        sed -i '/revert_new_ibus_keyboard_use.patch/d' "${srcdir}/debian/patches/series"
      # Arch Linux uses PackageKit
        sed -i '/05_run_update_manager.patch/d' "${srcdir}/debian/patches/series"
      # Ubuntu specific
        sed -i '/10_keyboard_layout_on_unity.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/52_region_language.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/53_use_ubuntu_help.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/56_use_ubuntu_info_branding.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/62_update_translations_template.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/92_ubuntu_system_proxy.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/revert_git_info_packagekit_api.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/ubuntu_region_packagekit.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/ubuntu_region_install_dialog.patch/d' "${srcdir}/debian/patches/series"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying ${i} ..."
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  ### Translations are currently incomplete ###

  # The title changed from "System Settings" to "Settings" in GNOME 3.8. We'll
  # revert it since it breaks all of Ubuntu's translations
#  patch -p1 -i "${srcdir}/0001_Rename_back_to_System_Settings.patch"

#  msg "Merging translations from ${_translations}"
#  rm -f po/LINGUAS po/*.pot
#  mv "${srcdir}"/po/*.pot po/
#  for i in "${srcdir}"/po/*.po; do
#    FILE=$(sed -n "s|.*/gnome-control-center-2.0-||p" <<< ${i})
#    mv ${i} po/${FILE}
#    echo ${FILE%.*} >> po/LINGUAS
#  done
}

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname \
    --disable-static \
    --enable-systemd

  # https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Fix a warning
  chown 102:0 "${pkgdir}/usr/share/polkit-1/rules.d/"
  chmod 700 "${pkgdir}/usr/share/polkit-1/rules.d/"

  # Ubuntu specific files
  install -dm755 "${pkgdir}/usr/share/indicators/session/applications/"
  ln -s '/usr/share/applications/gnome-control-center.desktop' \
    "${pkgdir}/usr/share/indicators/session/applications/gnome-control-center.desktop"

  # Install legal notice
  install -m644 "${srcdir}/debian/searchingthedashlegalnotice.html" \
    "${pkgdir}/usr/share/gnome-control-center/"

  # Make the Region panel show up in Unity
  desktop-file-install \
    --dir "${pkgdir}/usr/share/applications/" \
    --add-only-show-in=Unity \
    "${pkgdir}/usr/share/applications/gnome-region-panel.desktop"
}

# vim:set ts=2 sw=2 et:
