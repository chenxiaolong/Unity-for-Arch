# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-control-center-ubuntu
_ppa_rel=0ubuntu1~raring2
pkgver=3.8.1
pkgrel=101
_translations=20130418
pkgdesc="The Control Center for GNOME with Ubuntu's patches"
arch=('i686' 'x86_64')
groups=('gnome')
url="http://www.gnome.org"
license=('GPL')
depends=('accountsservice' 'cups-pk-helper' 'gnome-bluetooth' 'gnome-desktop' 'gnome-menus' 'gnome-online-accounts' 'gnome-settings-daemon-ubuntu' 'gsettings-desktop-schemas' 'gtk3-ubuntu' 'libgtop' 'libsocialweb' 'network-manager-applet' 'sound-theme-freedesktop' 'upower' 'libpwquality' 'libsystemd' 'archlinux-artwork' 'webkitgtk3' 'ibus' 'system-config-printer' 'colord-gtk')
makedepends=('gnome-common' 'gnome-doc-utils' 'intltool' 'docbook-xsl' 'patchutils')
optdepends=('mesa-demos: provides glxinfo for graphics information'
            'gnome-color-manager: for color management tasks')
provides=("gnome-control-center=${pkgver}" 'FEATURE-libgnome-control-center')
conflicts=('gnome-control-center')
options=('!libtool' '!emptydirs')
install=gnome-control-center.install
source=("http://download.gnome.org/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/g/gnome-control-center/gnome-control-center_${pkgver}-${_ppa_rel}.debian.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-gnome-control-center.tar.gz")
sha512sums=('edbe41bacd82f4b8ba06dcf5c6660fe695239175b062a9590327b7c05506ab25b6671d222f18fa80b012c35a91669ef73a36f559caed36bcac1aa1b7481a759e'
            '7773818a6959c495a349a80e2dbe3b1d5f94756b637d0fbd7dd423e53af9471679ebe172aa220e9163775c9bc8feb6d4c965a1f57e10ddbe63fbd91e366f5c70'
            'f08d00d02170d55b9908ae8aef2a13f9cbd393168bc70b28e31e8f35ab690903992ba380afbf7ef935d13ce81d3c3bec9b9db2711e2bbc7d1a245e700df5bdbc')

prepare() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"
  # Apply Arch Linux's patches
  #  patch -p1 -i "${srcdir}/pulse3.0.patch"


  # Apply Ubuntu's patches
    # Disable patches
      # Ubuntu is too lazy to port their patches to the latest version of IBus
        sed -i '/revert_new_ibus_keyboard_use.patch/d' "${srcdir}/debian/patches/series"
      # The gnome-settings-daemon-ubuntu patch for this conflicts with Arch
      # Linux's patches
        sed -i '/99_add_lock-on-suspend.patch/d' "${srcdir}/debian/patches/series"
      # Arch Linux uses PackageKit
        sed -i '/05_run_update_manager.patch/d' "${srcdir}/debian/patches/series"
      # Ubuntu specific
        sed -i '/10_keyboard_layout_on_unity.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/50_ubuntu_systemwide_proxy.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/52_region_language.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/53_use_ubuntu_help.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/56_use_ubuntu_info_branding.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/62_update_translations_template.patch/d' "${srcdir}/debian/patches/series"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying ${i} ..."
    if [ "x${i}" = "xunity_notice_info.patch" ]; then
      cat "${srcdir}/debian/patches/${i}" | \
        filterdiff --exclude gnome-control-center-3.7.91/configure.ac | patch -p1
      # Base 64 because the tabs will screw up bash's parser
      cat << EOF | base64 -d | patch -p0
LS0tIGNvbmZpZ3VyZS5hYy5vcmlnCTIwMTMtMDQtMjIgMTg6MDQ6MzMuMzg3MjIyMjgwICswMjAw
CisrKyBjb25maWd1cmUuYWMJMjAxMy0wNC0yMiAxODowNzoxNy40NjM0OTg0NzQgKzAyMDAKQEAg
LTEzNyw3ICsxMzcsNyBAQAogICAgICAgICAgICAgICAgICAgZ2RrLXBpeGJ1Zi0yLjAgPj0gJEdE
S1BJWEJVRl9SRVFVSVJFRF9WRVJTSU9OKQogUEtHX0NIRUNLX01PRFVMRVMoRElTUExBWV9QQU5F
TCwgJENPTU1PTl9NT0RVTEVTIGdub21lLWRlc2t0b3AtMy4wID49IDMuMS4wKQogUEtHX0NIRUNL
X01PRFVMRVMoSU5GT19QQU5FTCwgJENPTU1PTl9NT0RVTEVTIGxpYmd0b3AtMi4wIGdsCi0JCSAg
cG9sa2l0LWdvYmplY3QtMSA+PSAkUE9MS0lUX1JFUVVJUkVEX1ZFUlNJT04pCisJCSAgcG9sa2l0
LWdvYmplY3QtMSA+PSAkUE9MS0lUX1JFUVVJUkVEX1ZFUlNJT04gd2Via2l0Z3RrLTMuMCkKIFBL
R19DSEVDS19NT0RVTEVTKEtFWUJPQVJEX1BBTkVMLCAkQ09NTU9OX01PRFVMRVMKICAgICAgICAg
ICAgICAgICAgIGdub21lLWRlc2t0b3AtMy4wID49ICRHTk9NRV9ERVNLVE9QX1JFUVVJUkVEX1ZF
UlNJT04KICAgICAgICAgICAgICAgICAgIHgxMSkK
EOF
    else
      patch -p1 -i "${srcdir}/debian/patches/${i}"
    fi
  done

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/gnome-control-center-2.0-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-systemd \
    --with-libsocialweb \
    --disable-update-mimedb \
    --without-cheese

  # https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Fix a warning
  chown 102:0 "${pkgdir}/usr/share/polkit-1/rules.d/"
  chmod 700 "${pkgdir}/usr/share/polkit-1/rules.d/"

  # Ubuntu specific files
  install -dm755 "${pkgdir}/usr/share/indicators/session/applications/"
  ln -s '/usr/share/applications/gnome-control-center.desktop' \
    "${pkgdir}/usr/share/indicators/session/applications/gnome-control-center.desktop"

  # Install legal notice
  install -m644 "${srcdir}/debian/searchingthedashlegalnotice.html" \
    "${pkgdir}/usr/share/gnome-control-center/"
}

# vim:set ts=2 sw=2 et:
