# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-control-center-ubuntu
_ppa_rel=0ubuntu1~trusty1
_ppa_ver=3.12.0
pkgver=3.12.1
pkgrel=1
pkgdesc="The Control Center for GNOME and Unity"
arch=('i686' 'x86_64')
groups=('gnome' 'unity')
url="http://www.gnome.org"
license=('GPL')
depends=('accountsservice' 'cups-pk-helper' 'gnome-bluetooth' 'gnome-desktop' 'gnome-menus' 'gnome-online-accounts' 'gnome-settings-daemon-ubuntu' 'gsettings-desktop-schemas' 'gtk3-ubuntu' 'libgtop' 'network-manager-applet' 'sound-theme-freedesktop' 'upower' 'libpwquality' 'gnome-color-manager' 'smbclient' 'libmm-glib' 'libgnomekbd' 'grilo' 'clutter-gtk' 'webkitgtk3' 'unity-control-center')
makedepends=('gnome-common' 'gnome-doc-utils' 'intltool' 'docbook-xsl' 'modemmanager' 'vala' 'desktop-file-utils')
optdepends=('system-config-printer: Printer settings'
            'gnome-user-share: Bluetooth and WebDAV file sharing'
            'rygel: media sharing'
            'vino: screen sharing'
            'openssh: remote login'
            'ntp: network time sync')
provides=("gnome-control-center=${pkgver}")
conflicts=('gnome-control-center')
options=('!emptydirs')
install=gnome-control-center.install
source=("http://download.gnome.org/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/g/gnome-control-center/gnome-control-center_${_ppa_ver}-${_ppa_rel}.debian.tar.gz"
        '0001-Remove-overlay-scrollbar-from-GTK_MODULES.patch'
        '0002-Reimplement-support-for-loading-external-panels.patch')
sha512sums=('35b765fc11182b0f7eec6bc31934f51ccbf01dde9f2e7deed6c84ca9811bd0a00783d75bd3fab7004fe41cc29740b6d6e0a7f3c6391a482eefcab0129d0278fe'
            '44c56821b55746df8bfb6ca0aa303f63353f0350d23617d5f245d777c39c3e5989db41006dfffbaf131bb1b43a6c474075acff14f9ea5c10e34c4af9fb89bd97'
            '5f2c2188b6f4e1d4999e031ba8638bac29ba6688f331ad8cfc6c3fe4c104eeebaf3a53cea5ecbd5cc7c925cfad3138a86b893f996df2ae035b61ebcdc6e862d1'
            '0e205e82d74d47a15bb802bcac66328dd5cd69b8a4ac92420bdc6138dc124b1fec6757ee04cc9391e0d7e67360762bfbf6ee104a2b3a6736862c8ae016248e88')

prepare() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  patch -p1 -i "${srcdir}/0001-Remove-overlay-scrollbar-from-GTK_MODULES.patch"
  patch -p1 -i "${srcdir}/0002-Reimplement-support-for-loading-external-panels.patch"

  # Apply Ubuntu's patches
    # Disable patches
      # Why?
        sed -i '/revert_git_new_bluetooth_panel.patch/d' "${srcdir}/debian/patches/series"
      # Arch Linux uses PackageKit
        sed -i '/05_run_update_manager.patch/d' "${srcdir}/debian/patches/series"
      # Ubuntu specific
        sed -i '/52_region_language.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/52_ubuntu_language_list_mods.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/53_use_ubuntu_help.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/55_user_accounts_hide_controls.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/56_use_ubuntu_info_branding.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/59_install_gcm_components_on_demand.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/61_workaround_online_account.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/62_update_translations_template.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/91_unity_no_printing_panel.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/92_ubuntu_system_proxy.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/revert_git_info_packagekit_api.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/ubuntu_region_packagekit.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/zz_add_fallback_panels_dir.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/ubuntu_external_panels.patch/d' "${srcdir}/debian/patches/series"
      # There are currently no packages compatible with this library (assuming it even works)
        sed -i '/revert_git_drop_library.patch/d' "${srcdir}/debian/patches/series"
      # We have gnome-bluetooth 3.12
        sed -i '/revert_new_bluetooth.patch/d' "${srcdir}/debian/patches/series"
      # We also have BlueZ 5
        sed -i '/revert-bluetooth-BlueZ5-interface.patch/d' "${srcdir}/debian/patches/series"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying ${i} ..."
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/${pkgname} \
    --disable-static \
    --disable-silent-rules

  # https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Fix a warning
  chown 102:0 "${pkgdir}/usr/share/polkit-1/rules.d/"
  chmod 700 "${pkgdir}/usr/share/polkit-1/rules.d/"

  # Ubuntu specific files
  install -dm755 "${pkgdir}/usr/share/indicators/session/applications/"
  ln -s '/usr/share/applications/gnome-control-center.desktop' \
    "${pkgdir}/usr/share/indicators/session/applications/gnome-control-center.desktop"

  # Install legal notice
  install -m644 "${srcdir}/debian/searchingthedashlegalnotice.html" \
    "${pkgdir}/usr/share/gnome-control-center/"

  # Make the Region panel show up in Unity
  desktop-file-install \
    --dir "${pkgdir}/usr/share/applications/" \
    --add-only-show-in=Unity \
    "${pkgdir}/usr/share/applications/gnome-region-panel.desktop"

  # Hide Displays panel in Unity (it only works with gnome-shell)
  desktop-file-install \
    --dir "${pkgdir}/usr/share/applications/" \
    --remove-only-show-in=Unity \
    "${pkgdir}/usr/share/applications/gnome-display-panel.desktop"
}

# vim:set ts=2 sw=2 et:
