# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=unity-lens-video
_actual_ver=0.3.14
_extra_ver=daily13.04.15
_translations=20130418
pkgver=${_actual_ver}.${_extra_ver}
pkgrel=102
pkgdesc="Unity Video lens"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-lens-videos"
license=('GPL')
depends=('dee' 'json-glib' 'libgee' 'libsoup-gnome' 'libunity' 'libzeitgeist' 'unity-lens-music')
makedepends=('gnome-common' 'intltool' 'vala')
checkdepends=('xorg-server-xvfb')
provides=("unity-scope-video-remote=${pkgver}")
conflicts=('unity-scope-video-remote')
replaces=('unity-scope-video-remote')
groups=('unity')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-lens-video_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-unity-lens-video.tar.gz")
sha512sums=('0a2845af53f86308ffc9b1f3ab92fa1638421e313e750a44015acd87a660ed91fff9f71898c56073f9a70df2f6ff414f0912dc305496511420fbbaafab0fa292'
            'cac380942dd525e405dcac5c348e0aac8c734cb5ee2d2ef034a1a4a0acbeb54dc5598dc40ab7e4b0fcbe28eec322663b15a1df09fc36aec091c1f959ec67b700')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib --enable-headless-tests
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
