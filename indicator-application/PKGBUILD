# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=zesty
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-application
_actual_ver=12.10.1
_extra_ver=+17.04.20161021
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=2
epoch=1
pkgdesc="A library and indicator to take menus from applications and place them in the panel"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-application"
license=(GPL)
depends=(dbus-glib libappindicator-gtk3 libdbusmenu-gtk3 libindicator-gtk3
         json-glib)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-application_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001_autoconf.patch)
sha512sums=('ec8fe863a683850cd4df734cdb31169f1fc20cd014058ab43cda5319e230dca57607397613896523cd87405e1cedeaab04c7c118112be1e5e9b5e8b89f37453a'
            '7ef9f2eab8dd17d8957b706ba479c38f5f9e74a69e77af7c8e318809c67b4aea214433f0364741fc4fcbfaefd6bbff3e370e24048cd23f2e09ebe6bca32b1511')

prepare() {
    patch -p1 -i 0001_autoconf.patch

    autoreconf -vfi
}

build() {
    ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static
    make
}

package() {
    make DESTDIR="${pkgdir}/" install
}
