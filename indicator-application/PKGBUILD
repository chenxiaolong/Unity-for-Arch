# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-application
_actual_ver=12.10.1
_extra_ver=daily13.01.25
pkgver=${_actual_ver}${_extra_ver}
pkgrel=101
pkgdesc="A library and indicator to take menus from applications and place them in the panel"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-application"
license=('GPL')
depends=('dbus-glib' 'libappindicator3' 'libdbusmenu-gtk3' 'libindicator3')
checkdepends=('dbus-test-runner' 'xorg-server-xvfb')
groups=('unity')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-application_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_autoconf.patch'
        'revert_r239.patch')
sha512sums=('bb5328a7e2dde5dea6568963defd977d07a6ebe0d427c91f369034979a88947ebe3d6f95ca0e9c89bf120179363b73a1becd7e300f47e52077fa6bb27b5ae6fb'
            '7ef9f2eab8dd17d8957b706ba479c38f5f9e74a69e77af7c8e318809c67b4aea214433f0364741fc4fcbfaefd6bbff3e370e24048cd23f2e09ebe6bca32b1511'
            '4b1bea8a0bc56bd1fabe0215dcd1f76aa7fabf094f70ec0bcfaf7decd8c8c9b4e50cd0852ba597cc9946e1e930222fe2d1d86777404a052b8dddf18b51b4cde2')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_autoconf.patch"
  patch -p0 -i "${srcdir}/revert_r239.patch"

  autoreconf -vfi
  ./configure --prefix=/usr --libexecdir=/usr/share/${pkgname} --disable-static
  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
