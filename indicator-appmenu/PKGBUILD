# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-appmenu
_actual_ver=13.01.0
_extra_ver=daily13.03.28
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="An indicator to host the menus from an application"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-appmenu"
license=('GPL')
groups=('unity')
depends=('dconf' 'libappindicator3' 'libindicator3' 'libdbusmenu-gtk3' 'libbamf3')
makedepends=('gtk-doc' 'intltool' 'vala')
optdepends=('appmenu-gtk2: support for GTK+ 2 apps'
            'appmenu-gtk3: support for GTK+ 3 apps'
            'appmenu-qt: support for Qt apps')
provides=('indicator-appmenu-tools')
conflicts=('indicator-appmenu-tools')
options=('!libtool' '!emptydirs')
install="${pkgname}.install"
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-appmenu_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_autoconf.patch'
        '0002_False_GLib_Requirement.patch')
sha512sums=('a8eb9d93c8b7373ebcfdfa37b0588925a83c2a3475a99046bb3a9c365ce50d16acf525d6c02c1430a0320aa3bb5d8d3ff69ece9450ba42d98c7ebba16b3720c1'
            '31b83e433824bd7acd7be93bfd60932114eff344fea77c8ea1125272d5881fd61c1609d4fbf24de0cc8838e76d8ac405e7a19162e3ce2021b6b5b6dbb61c31f8'
            '8275d0d27c1f3180a2e5ec936c37570fd8ff98c3e5fdae110a8dc8be78fc6db6c8e5c984aa501b0790407e8e5c42e4afda47d1cb463ed41f41949baec313fa25')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Autoconf fix
  patch -p1 -i "${srcdir}/0001_autoconf.patch"

  # GLib 2.35.* is not actually required (due to the splitting away of hud)
  patch -p1 -i "${srcdir}/0002_False_GLib_Requirement.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  gtkdocize
  autoreconf -vfi
  intltoolize -f

  # Disable -Werror
  export CFLAGS="${CFLAGS} -Wno-error"

  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
