# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-appmenu
_actual_ver=13.01.0
_extra_ver=+15.04.20150114
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
epoch=1
pkgdesc="An indicator to host the menus from an application"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-appmenu"
license=(GPL)
groups=(unity)
depends=(dconf libappindicator3 libindicator3 libdbusmenu-gtk3 bamf-ubuntu)
makedepends=(gtk-doc intltool vala)
optdepends=('unity-gtk-module: support for GTK+ 2 and GTK+ 3 apps'
            'appmenu-qt: support for Qt apps')
options=(!emptydirs)
install="${pkgname}.install"
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-appmenu_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001_autoconf.patch)
sha512sums=('1cebe83e74b110dac48d581afd0f64643c2981e2ab984d73ea4797ca1040076aaa503a6564a4f86611bb75348512426d95f3df962a7c519e0d3e9a616cb8b911'
            '31b83e433824bd7acd7be93bfd60932114eff344fea77c8ea1125272d5881fd61c1609d4fbf24de0cc8838e76d8ac405e7a19162e3ce2021b6b5b6dbb61c31f8')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  # Autoconf fix
  patch -p1 -i ../0001_autoconf.patch
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS+=" -Wno-error"

  gtkdocize
  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}/"

  make DESTDIR="${pkgdir}/" install
}
