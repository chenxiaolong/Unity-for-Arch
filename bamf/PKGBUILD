# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=bamf
pkgname=('bamfdaemon' 'libbamf' 'libbamf3')
pkgver=0.2.116
pkgrel=100
pkgdesc="Removes the headache of applications matching into a simple DBus daemon and c wrapper library"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
makedepends=('gobject-introspection' 'libwnck3' 'libwnck' 'libgtop' 'glib2-ubuntu>=2.31.14.0ubuntu1' 'gnome-common')
groups=('unity')
options=(!libtool)
install=${pkgbase}.install
source=("https://launchpad.net/bamf/${pkgver%.*}/${pkgver}/+download/bamf-${pkgver}.tar.gz")
sha512sums=('c0a696a5aacae949e1943ad04ab31d55768324165e5252bb4b0777abda8280d0db602c39ad47cd789e4c1d48ab76c38c393185ca33111b151f48ac5000f427a7')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  #sed -i 's/[ \t]*-Werror[ \t]*/ /g' configure.in
  sed -i 's/-Werror/-Wno-error=implicit-function-declaration/g' configure.in

  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  # Documentation will go in the GTK 2 package
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --with-gtk=2 --disable-static --enable-gtk-doc
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --with-gtk=3 --disable-static
  make ${MAKEFLAGS}
  popd
}

# Disable tests: they require Xorg to be running
#check() {
#  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk2"
#  make check
#
#  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk3"
#  make check
#}

package_bamfdaemon() {
  pkgdesc="Window matching library daemon"
  depends=('dbus-glib' 'libwnck3' 'libgtop' 'glib2-ubuntu>=2.31.14.0ubuntu1')
  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk3" # Doesn't matter which one

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/"
  install -dm755 "${pkgdir}/usr/share/dbus-1/services/"
  install -m755 'src/bamfdaemon' "${pkgdir}/usr/lib/${pkgbase}/"
  install -m644 'data/org.ayatana.bamf.service' "${pkgdir}/usr/share/dbus-1/services/"
}

package_libbamf() {
  pkgdesc="Window matching library for GTK 2"
  depends=('bamfdaemon' 'gtk2-ubuntu' 'glib2-ubuntu>=2.31.14.0ubuntu1')

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk2"

  make DESTDIR="${pkgdir}/" install

  # Remove bamfdaemon from this package
  rm -rvf "${pkgdir}/usr/lib/${pkgbase}/"
  rm -rvf "${pkgdir}/usr/share/dbus-1/"
  rm -rvf "${pkgdir}/usr/share/"
}

package_libbamf3() {
  pkgdesc="Window matching library for GTK 3"
  depends=('bamfdaemon' 'gtk3-ubuntu' 'glib2-ubuntu>=2.31.14.0ubuntu1')
  provides=("bamf=${pkgver}")
  conflicts=('bamf')

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk3"

  make DESTDIR="${pkgdir}/" install

  # Remove bamfdaemon from this package
  rm -rvf "${pkgdir}/usr/lib/${pkgbase}/"
  rm -rvf "${pkgdir}/usr/share/dbus-1/"
}

# vim:set ts=2 sw=2 et:
