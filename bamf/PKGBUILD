# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=bamf
pkgname=('bamfdaemon' 'libbamf3')
_actual_ver=0.4.0
_extra_ver=daily13.04.03
pkgver=${_actual_ver}${_extra_ver}
pkgrel=101
pkgdesc="Removes the headache of applications matching into a simple DBus daemon and c wrapper library"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
makedepends=('dbus-glib' 'gnome-common' 'gobject-introspection' 'gtk-doc' 'libgtop' 'libunity-webapps' 'libwnck3' 'vala')
checkdepends=('xorg-server-xvfb')
provides=('FEATURE-bamf-webapps')
groups=('unity')
options=('!libtool')
install=${pkgbase}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/bamf_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_Disable_Werror.patch')
sha512sums=('6af815d66b07ee4e15d43b1bcfba2f7ab197c76b3a768a549f9112e9d03d1a3a479ea7ac60f49533b6d209857e9713edbc31bfcb4aac2177be70cde1189fb277'
            'f1881b97d832d2ac3e4ad00392439023e3c562f2687d62d8dbf2d9f789dd147e1e51c0723899489c3d10c2048bda5f48f2e4f3e91b75d36810be2a74ba636625')

prepare() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_Disable_Werror.patch"
}

build() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  gtkdocize
  autoreconf -vfi
  
  if ! pacman -Q xorg-server-xvfb &>/dev/null; then
    ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static --enable-gtk-doc
  else
    ./configure --prefix=/usr --libexecdir=/usr/lib --disable-static --enable-gtk-doc --enable-headless-tests
  fi
  make
}

check() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  make check
}

package_bamfdaemon() {
  pkgdesc="Window matching library daemon"
  depends=('dbus-glib' 'libgtop' 'libunity-webapps' 'libwnck3')
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  install -dm755 "${pkgdir}/usr/lib/${pkgbase}/"
  install -dm755 "${pkgdir}/usr/share/dbus-1/services/"
  install -m755 'src/bamfdaemon' "${pkgdir}/usr/lib/${pkgbase}/"
  install -m644 'data/org.ayatana.bamf.service' "${pkgdir}/usr/share/dbus-1/services/"
}

package_libbamf3() {
  pkgdesc="Window matching library for GTK 3"
  depends=('bamfdaemon')
  provides=("bamf=${pkgver}")
  conflicts=('bamf')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install

  # Remove bamfdaemon from this package
  rm -rv "${pkgdir}/usr/lib/${pkgbase}/"
  rm -rv "${pkgdir}/usr/share/dbus-1/"
}

# vim:set ts=2 sw=2 et:
