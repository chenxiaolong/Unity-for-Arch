# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${_name}, repo=yakkety
# vercheck-launchpad: name=${_name}

_name=gsettings-ubuntu-touch-schemas
pkgname=gsettings-ubuntu-schemas
_actual_ver=0.0.5
_extra_ver=+16.04.20160307
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="GSettings desktop-wide schemas for Ubuntu"
arch=(any)
url="https://launchpad.net/gsettings-ubuntu-touch-schemas"
license=(LGPL)
groups=(unity)
depends=(glib2)
makedepends=(gnome-common intltool)
optdepends=('accountsservice: For accountsservice-related schemas')
options=(!emptydirs)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${_name}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        50-com.ubuntu.AccountsService.rules)
sha512sums=('863e7a8d70f96c831022c4385814c4aa211bf5b0c80605d565502e05bba8e714f09e080e92983c34183d8e5494bc96eb3420520cacb55278817d726fbb73525e'
            'f00605ff9a55c5d9c75146d28e2e8296a2e1468a504950c127668440bbe0758026dd1112ce5a91a7b342fb53f6c64211d5b55188642821b1b38dbfc862f248c0')

build() {
	cd "${_name}-${_actual_ver}${_extra_ver}"
	./autogen.sh \
        --prefix=/usr \
        --localstatedir=/var
	make
}

package() {
	cd "${_name}-${_actual_ver}${_extra_ver}"
	make DESTDIR="${pkgdir}/" install

    install -dm750 "${pkgdir}"/usr/share/polkit-1/rules.d/
    install -m644 "${srcdir}"/50-com.ubuntu.AccountsService.rules \
        "${pkgdir}"/usr/share/polkit-1/rules.d/

    rm -v "${pkgdir}"/var/lib/polkit-1/localauthority/10-vendor.d/50-com.ubuntu.AccountsService.pkla
}
