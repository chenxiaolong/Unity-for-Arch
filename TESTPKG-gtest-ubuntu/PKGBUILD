# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=gtest-ubuntu
pkgver=1.6.0
_ubuntu_rel=1ubuntu6
pkgrel=4
pkgdesc="Google's framework for writing C++ tests"
arch=('any')
url="http://code.google.com/p/googletest/"
license=('BSD')
groups=('unity-tests')
makedepends=('cmake' 'python2')
provides=("gtest=${pkgver}")
conflicts=('gtest')
source=("http://googletest.googlecode.com/files/gtest-${pkgver}.zip"
        "https://launchpad.net/ubuntu/+archive/primary/+files/gtest_${pkgver}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('f4718dfbfa3339bb9449c3f14e5b44ae405ea7df64c10a0957a6300985b71c4642981d069a1382e27ae041a4e2873527a9e442aff978447e795a190f99fac115'
            '99a87470db7b6911f25551801d5be9151b8b65f206d8014ec3a39c5851bbe450e43d8162da11bd9a18207e0a6bf09eb4182820564f6b1c7702076a8d41693631')

build() {
  cd "${srcdir}/gtest-${pkgver}"

  find -type f -exec chmod 644 {} \+
  find -type f -name '*.py' -exec sed -i 's|^\(#!.*python$\)|\12|g' {} \+

  # Apply Ubuntu's patches
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  mkdir build
  cd build
  unset CFLAGS CXXFLAGS LDFLAGS
  cmake .. -Dgtest_build_tests=ON -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

check() {
  cd "${srcdir}/gtest-${pkgver}/build"
  make test
}

package() {
  cd "${srcdir}/gtest-${pkgver}"

  install -dm755       "${pkgdir}/usr/include/"
  cp -a include/gtest/ "${pkgdir}/usr/include/"
  install -dm755       "${pkgdir}/usr/src/gtest/"
  cp -a src/           "${pkgdir}/usr/src/gtest/"
  cp -a CMakeLists.txt "${pkgdir}/usr/src/gtest/"
  cp -a cmake/         "${pkgdir}/usr/src/gtest/"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
