# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=unity-lens-music
_actual_ver=6.9.0
_extra_ver=+13.10.20131011
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=103
pkgdesc="Music, in the dash"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-place-music"
license=('GPL')
depends=('gstreamer' 'gst-plugins-base' 'libgee06' 'libnotify' 'liboauth' 'libsoup' 'libunity' 'tdb' 'json-glib')
makedepends=('vala' 'intltool')
groups=('unity')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-lens-music_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001-Remove-Ubuntu-One-support-service-is-dead.patch')
sha512sums=('3f4479c05655220dc952c58c5b9183d83d144c4b9a72f52de43b6a6d02429fb83b42865a3502b133d5b5f65470cc26744a3a27c6d4c7d29c68f0166caefaee4b'
            'e50ac27fa317d845d6a9dcf75b022503d666a65e2463e4c535910555875158003db37d25f8ef4399e9657364df4e96398033f3ef75867d41130f691e0d8c6578')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001-Remove-Ubuntu-One-support-service-is-dead.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  aclocal --install --force
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
