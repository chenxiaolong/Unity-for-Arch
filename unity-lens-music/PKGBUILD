# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=unity-lens-music
_actual_ver=6.8.1
_extra_ver=daily13.02.07
pkgver=${_actual_ver}.${_extra_ver}
pkgrel=100
pkgdesc="Music, in the dash"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-place-music"
license=('GPL')
depends=('libunity' 'gstreamer' 'gst-plugins-base' 'liboauth' 'libsoup' 'tdb')
makedepends=('vala' 'intltool')
groups=('unity')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-lens-music_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('9d77ee64cec637c5c51787072c708d9398b65f7199014efd794b565995647f4140321a1db96d6a99d8ac9039d46857e8f23abb9f2d66f8bf557e32982fce2813')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  aclocal --install --force
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
