# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>

pkgname=gtk3-ubuntu
_ubuntu_rel=0ubuntu3.1
_ubuntu_ver=3.6.0
_actual_ver=3.6.1
pkgver=${_actual_ver}.ubuntu${_ubuntu_ver}.${_ubuntu_rel}
pkgrel=100
pkgdesc="GObject-based multi-platform toolkit (v3)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=arch_gtk3.install
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 'colord' 'at-spi2-atk')
makedepends=('gobject-introspection')
provides=("gtk3=${pkgver%.*.*}")
conflicts=('gtk3')
options=('!libtool' '!docs')
backup=('etc/gtk-3.0/settings.ini')
license=('LGPL')
source=("http://ftp.gnome.org/pub/GNOME/sources/gtk+/${_actual_ver%.*}/gtk+-${_actual_ver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/gtk+3.0_${_ubuntu_ver}-${_ubuntu_rel}.debian.tar.gz"
        'arch_settings.ini')
sha512sums=('9b6927c48550ac08860b20901880e944dc151b73b6bb10dbeff04eb17308fb15d064ec880c744f99e3139a41535c2dca31c92be06e4a69a00e267875991cedb1'
            'db902dea732ea27f2f802cc3e873024390e27f5e2b191657929234949757627cb0ef521c30d65ea486a261324ead65b3ca8f6c9c868c5572cf4f1bba2d7851cc'
            '087bf853cb9f6ee8269c60041555ee1546a2d10fbdbe820710c77a74bb9c34735abe8b061e2a0322246932d7a72b21953682fcdca79b09fd555307abb1d88699')

build() {
  cd "${srcdir}/gtk+-${_actual_ver}"

  # Apply Ubuntu Patches
    # Disable patches
      # No multiarch
        sed -i '/061_multiarch_module_fallback.patch/d' "${srcdir}/debian/patches/series"
      # Only causes problems in Ubuntu
        sed -i '/revert_git_a11y_stopped.patch/d' "${srcdir}/debian/patches/series"
      # Merged upstream
        sed -i '/git_use_right_display.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/git_iconview_render_item_select.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/git_appchooser_check_should_show.patch/d' "${srcdir}/debian/patches/series"
        sed -i '/git_process_all_updates_synchronous.patch/d' "${srcdir}/debian/patches/series"

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi

  CXX=/bin/false ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk2-dependency \
    --disable-schemas-compile \
    --enable-test-print-backend

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/gtk+-${_actual_ver}"
  make mkdir_p="/bin/mkdir -p" DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/arch_settings.ini" \
    "${pkgdir}/etc/gtk-3.0/settings.ini"
}

# vim:set ts=2 sw=2 et:
