# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=gtk3-ubuntu
_ppa_ver=3.8.0
_ppa_rel=0ubuntu1~build1
pkgver=3.8.1
pkgrel=100
pkgdesc="GObject-based multi-platform toolkit (v3)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=arch_gtk3.install
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 'colord' 'at-spi2-atk' 'wayland' 'libxkbcommon')
makedepends=('gobject-introspection' 'mesa')
checkdepends=('hicolor-icon-theme' 'xorg-server-xvfb')
provides=("gtk3=${pkgver}")
conflicts=('gtk3')
options=('!libtool' '!docs')
backup=('etc/gtk-3.0/settings.ini')
license=('LGPL')
source=("http://ftp.gnome.org/pub/GNOME/sources/gtk+/${pkgver%.*}/gtk+-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3/ubuntu/pool/main/g/gtk+3.0/gtk+3.0_${_ppa_ver}-${_ppa_rel}.debian.tar.gz"
        '0001_Add_broadway_symbols.patch'
        'arch_settings.ini')
sha512sums=('384c629f652452113d627967eca926ab66ee22ed6ed1b7d0d3f74bc8d8940cf82f0a91209d0d4fd28acf732404d2312d38992fbc47763e3baf453d9e9692ff08'
            '84b04b2c41a833d67463f23264b020e392fecf10b5f137bb683d64388ac5a1eb73f4bdc7aeddcb5aaca57b594cd69c79cf513a87501de786a89cec4a77ad44e1'
            'ee8a6b7b5f016dae5a588834842b584c1fbd955065469550ab33571d2302ca0ba3796265b4293e1f38a28fa00e1c1596cf438501ab782d3fd6412cac23025a00'
            '087bf853cb9f6ee8269c60041555ee1546a2d10fbdbe820710c77a74bb9c34735abe8b061e2a0322246932d7a72b21953682fcdca79b09fd555307abb1d88699')

prepare() {
  cd "${srcdir}/gtk+-${pkgver}"

  # Add Broadway sumbols to tests pass
  patch -p1 -i "${srcdir}/0001_Add_broadway_symbols.patch"

  # Apply Ubuntu Patches
    # Disable patches
      # Debian/Ubuntu's multiarch
        sed -i '/061_multiarch_module_fallback.patch/d' "${srcdir}/debian/patches/series"
      # Fedora's tracker is not compiled with FTS
      #  sed -i '/044_tracker_fts.patch/d' debian/patches/series

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying ${i} ..."
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/gtk+-${pkgver}"

  autoreconf -vfi

  #CXX=/bin/false ./configure \
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk2-dependency \
    --disable-schemas-compile \
    --enable-x11-backend \
    --enable-broadway-backend \
    --enable-wayland-backend \
    --enable-test-print-backend

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

# Okay, these tests are obviously untested. There's no reason GTK 3 needs to be
# installed for them to run.
check() {
  cd "${srcdir}/gtk+-${pkgver}"

  # Do the GTK developers even check that their tests work?
  rm ./tests/css/parser/background-shorthand.css
  rm ./tests/css/parser/background-shorthand-single.css
  rm ./tests/css/parser/declarations-valid-22.css
  rm ./tests/css/parser/declarations-valid-23.css
  rm ./tests/a11y/about.ui
  rm ./tests/a11y/combos.ui
  rm ./tests/a11y/menu.ui
  rm ./tests/a11y/notebook.ui
  rm ./tests/a11y/pickers.ui
  #make -j1 check
}

package() {
  cd "${srcdir}/gtk+-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/arch_settings.ini" \
    "${pkgdir}/etc/gtk-3.0/settings.ini"
}

# vim:set ts=2 sw=2 et:
