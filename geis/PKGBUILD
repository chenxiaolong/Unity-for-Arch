# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic
# vercheck-launchpad: name=${pkgname}

pkgname=geis
_actual_ver=2.2.16
_extra_ver=+15.04.20150304
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
epoch=1
pkgdesc="Implementation of the GEIS (Gesture Engine Interface and Support) interface."
arch=(i686 x86_64)
url="https://launchpad.net/geis"
license=(GPL LGPL)
depends=(grail xorg-xinput python3 dbus-core)
makedepends=(xorg-server-devel)
options=(!emptydirs)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/geis_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('3837c5433f53068233c2e3502a9c65434844c4be95cd3c22f83bbfb2c077001f265ded29bed9dc2ad113857d204ffbd55a4ddce97981323402f2452da09ac478')

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}
