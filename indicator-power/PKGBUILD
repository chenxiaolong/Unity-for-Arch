# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=zesty
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-power
_actual_ver=12.10.6
_extra_ver=+17.04.20161021
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=2
epoch=1
pkgdesc="An indicator to show the power status of your devices"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-power"
license=(GPL)
depends=(dconf libnotify upower unity-control-center)
makedepends=(cmake intltool python2)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-power_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Not-a-phone.patch)
sha512sums=('29027e5f6ce1ce83f0bff8042cfc45e935124d320e73b901a46cb275b67550a88396616948a38a5da52a58ec814bedd6adc2e1b3927fa46a61347cd49e559db2'
            'f7dc02d696025d28a2dcd1773e996b5b8303de63de6f3e4653f75494ca363d2b76ef4cb9debb2cc20f4a9a27a7957d1ff0110a0d5d1e0635f03b5d4a02272cd0')

prepare() {
    patch -p1 -i 0001-Not-a-phone.patch

    sed -ie '/NotShowIn=/d' data/indicator-power.desktop.in
}

build() {
    export CFLAGS+=" -Wno-error"

    rm -rf build; mkdir build; cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
        -Denable_tests=OFF
    make
}

package() {
    cd build

    make DESTDIR="${pkgdir}/" install

    find "${pkgdir}" -type f -name '*.a' -delete

    rm -rv "${pkgdir}"/usr/share/upstart/
}
