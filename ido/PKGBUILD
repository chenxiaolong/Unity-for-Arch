# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=ido
_actual_ver=12.10.3
_extra_ver=daily13.01.11
pkgver=${_actual_ver}${_extra_ver}
pkgrel=101
pkgdesc="Widgets and other objects used for indicators"
arch=('i686' 'x86_64')
url="https://launchpad.net/ido"
license=('LGPL')
depends=('gtk3-ubuntu')
groups=('unity')
options=('!libtool')
provides=('ido3')
conflicts=('ido3')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/ido_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('8d1d30b88fea94c09ef99d39250db65ea0c2761f984aacdf1a309a66b748a5c73c02b9f9fbbced669d53aa1aa8c870a573a89bcc17c51efedfe11960b00f02bd')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  ./configure --prefix=/usr --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
