# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=xorg-gtest
pkgver=0.7.0
_ubuntu_rel=0ubuntu5
pkgrel=3
pkgdesc="X.org Google Test Framework"
arch=('any')
url="https://launchpad.net/xorg-gtest"
license=('GPL')
groups=('unity-tests')
depends=('gtest-ubuntu' 'libx11' 'libxi' 'xf86-video-dummy')
makedepends=('doxygen' 'valgrind' 'xorg-server-devel')
source=("https://launchpad.net/xorg-gtest/trunk/${pkgver}/+download/xorg-gtest-${pkgver}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/xorg-gtest_${pkgver}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('8ba9b431f76f59c2e70aee1d84fce00e591c4951603a6b96ed4a75473173c980124fb09d0df4952a57f97406dcfb64e97c12ef23be87780e2543ec8e19063fb6'
            '3658d7966a2df37924b2c1341e09eb88e17d80d11205e4855b82d03867aff9cfa5fa2ee3f78b62ff53733315ebf7f70603fbe3dd20829f146f8a4f914c4ba0d0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Apply Ubuntu's patches
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  autoreconf -vfi
  ./configure --prefix=/usr
  make
  make doc
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Tests currently fail
  XORG_GTEST_CHILD_STDOUT=true make -k -j1 check || :
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  rm -rvf "${pkgdir}/usr/include/gtest/"
}

# vim:set ts=2 sw=2 et:
