# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Christopher Reimer <github@creimer.net>

pkgname='gtk2-ubuntu'
_ubuntu_rel=0ubuntu2
pkgver=2.24.18
pkgrel=101
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
makedepends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage'
             'shared-mime-info' 'cairo' 'libcups' 'gdk-pixbuf2' 'gobject-introspection')
options=('!libtool')
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-$pkgver.tar.xz
        https://launchpad.net/ubuntu/+archive/primary/+files/gtk+2.0_${pkgver}-${_ubuntu_rel}.debian.tar.gz
        xid-collision-debug.patch)
sha256sums=('6c963101272ff53bb5b01d21bfb86cebba5f0a68f31c5e10523c63f07e762385'
            'e2e5b9eeeb015bb533376ee4ae5f829b9df20f93707bece6b4cb00db4fa920c2'
            'd758bb93e59df15a4ea7732cf984d1c3c19dff67c94b957575efea132b8fe558')

prepare() {
  cd gtk+-$pkgver

  # Apply Ubuntu Patches
    # Enable patches
      # Implement gtk-shell-shows-menubar from gtk3
      # Dependency check: grep -R gtk-shell-shows-menubar * | cut -d/ -f1
        echo 'gtk-shell-shows-menubar.patch' > "${srcdir}/debian/patches/series"
      # liboverlay-scrollbar.so: undefined symbol: ubuntu_gtk_set_use_overlay_scrollbar
      # Dependency check: grep -R ubuntu_gtk_get_use_overlay_scrollbar * | cut -d/ -f1
        echo '100_overlay_scrollbar_loading.patch' >> "${srcdir}/debian/patches/series"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
}

build() {
    cd gtk+-$pkgver
    patch -Np1 -i ../xid-collision-debug.patch

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-xinput=yes

    # https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    pkgdesc="GTK+ is a multi-platform toolkit (v2)"
    install=gtk2.install
    depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'shared-mime-info' 'cairo' 'libcups' 'gtk-update-icon-cache')
    backup=(etc/gtk-2.0/gtkrc)
    replaces=('gtk2-docs')

    cd gtk+-$pkgver

    make DESTDIR="$pkgdir" install
    sed -i "s#env python#env python2#" $pkgdir/usr/bin/gtk-builder-convert
    echo 'gtk-fallback-icon-theme = "gnome"' > "$pkgdir/etc/gtk-2.0/gtkrc"
    #split this out to use with gtk3 too
    rm $pkgdir/usr/bin/gtk-update-icon-cache
}
