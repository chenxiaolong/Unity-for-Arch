# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=nautilus-ubuntu
_ubuntu_rel=0ubuntu15
_actual_ver=3.6.3
pkgver=${_actual_ver}.${_ubuntu_rel}
pkgrel=101
pkgdesc="The GNOME shell and file manager with Ubuntu's patches"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libexif' 'gnome-desktop' 'exempi' 'gvfs' 'desktop-file-utils' 'gnome-icon-theme' 'dconf' 'libtracker-sparql' 'libunity' 'gtk3-ubuntu' 'libzeitgeist' 'libnotify')
makedepends=('intltool' 'gobject-introspection')
groups=('gnome')
provides=("nautilus=${_actual_ver}")
conflicts=('nautilus')
url="http://www.gnome.org"
options=('!libtool' '!emptydirs')
install=nautilus.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${_actual_ver%.*}/${pkgname%-*}-${_actual_ver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/nautilus_${_actual_ver}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('f90ed3253889764dc3799e53555a9d6e84876b5d5ea020ba34b643df460f703acbba85bba0cf468ea7d614ec954b49304e46a27d46459b4aaa7e53ecd9467895'
            'eb2abd5d6636dd1cdd99d8f45de675ed4d80ad5577ed3a2ab79bd14ce026e283cbd02db60c3269b7556053b418969dc054662516067300908b6f8a8e17f72604')

prepare() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"

  # Apply Ubuntu's patches

  # Disable patches
    # Don't use Ubuntu help
      sed -i '/15_use-ubuntu-help.patch/d' "${srcdir}/debian/patches/series"
    # Don't use launchpad-integration
      sed -i '/01_lpi.patch/d' "${srcdir}/debian/patches/series"
    # Enable after Arch gets GNOME 3.8
      sed -i '/git_restore_from_missing_directories.patch/d' "${srcdir}/debian/patches/series"

  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  # For updating translations in desktop files
  ln -s "${srcdir}/debian" .
}

build() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --libexecdir=/usr/lib/nautilus \
    --disable-nst-extension \
    --disable-update-mimedb \
    --disable-packagekit \
    --disable-schemas-compile

  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${_actual_ver}"
  make DESTDIR="${pkgdir}" install

  # Ubuntu specific stuff
  install -dm755 "${pkgdir}/usr/share/pixmaps/"
  install -dm755 "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/debian/nautilus.xpm" "${pkgdir}/usr/share/pixmaps/"
  install -m644 "${srcdir}/debian/mount-archive.desktop" "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/debian/nautilus-folder-handler.desktop" "${pkgdir}/usr/share/applications/"
}

# vim:set ts=2 sw=2 et:
