# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgbase%-*}, repo=zesty
# vercheck-archlinux: name=${pkgbase%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgbase%-*}, majorver=3.22
# vercheck-ppa: name=${pkgbase%-*}, url=ppa:gnome3-team/gnome3-staging

_use_ppa=true

pkgbase=nautilus-ubuntu
pkgname=(nautilus-ubuntu libnautilus-extension-ubuntu)
_ppa_rel=1ubuntu0~yakkety1
pkgver=3.22.2
pkgrel=1
pkgdesc="Default file manager for GNOME and Unity"
url="https://wiki.gnome.org/Apps/Nautilus"
arch=(i686 x86_64)
license=(GPL)
depends=(libexif gnome-desktop exempi gvfs dconf libtracker-sparql
         nautilus-sendto gnome-autoar libunity libzeitgeist)
makedepends=(intltool gobject-introspection python packagekit python2 gnome-common git gtk-doc)
options=(!emptydirs)
_commit=ce0c0bb5510577c9285512d2be52cba119903b96  # tags/3.22.1^0
source=("git://git.gnome.org/nautilus#commit=$_commit"
        "git://git.gnome.org/libgd")

if [[ "${_use_ppa}" == "true" ]]; then
    source+=("https://launchpad.net/~gnome3-team/+archive/ubuntu/gnome3-staging/+files/nautilus_${_ppa_ver:-${pkgver}}-${_ppa_rel}.debian.tar.xz")
else
    source+=("https://launchpad.net/ubuntu/+archive/primary/+files/nautilus_${_ubuntu_ver:-${pkgver}}-${_ubuntu_rel}.debian.tar.xz")
fi

sha512sums=('SKIP'
            'SKIP'
            '19589338d45b363a96435ac3b0bc672b7ec339282b158e33335981919d8a2683fc5a3bbb12933a1cdd60a37496de7f0d7340c03363503c264972d854bdac6a53')

pkgver() {
    cd nautilus
    git describe --tags | sed 's/-/+/g'
}

prepare() {
    cd nautilus

    git submodule init
    git config --local submodule.libgd.url "${srcdir}/libgd"
    git submodule update

    # Apply Ubuntu's patches

    # Disable patches
    sed -i \
        -e '/multiarch_fallback.patch/d' \
        -e '/15_use-ubuntu-help.patch/d' \
        -e '/08_clean_session_capplet.patch/d' \
        `# -e '/0003-Fix-white-desktop.patch/d'` \
        ../debian/patches/series

    for i in $(grep -v '#' ../debian/patches/series); do
        msg "Applying ${i} ..."
        patch -p1 -i "../debian/patches/${i}"
    done

    sed -i '/gnome_bg_set_draw_background/d' nautilus-desktop/nautilus-desktop-background.c

    ln -sf ../debian .

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd nautilus

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --libexecdir=/usr/lib/nautilus \
        --disable-update-mimedb \
        --disable-schemas-compile \
        --disable-selinux \
        --enable-gtk-doc \
        --enable-packagekit \
        --enable-introspection \
        --enable-tracker \
        --enable-unity

    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package_nautilus-ubuntu() {
    depends+=(libnautilus-extension-ubuntu)
    groups=(gnome unity)
    provides=("nautilus=${pkgver}")
    conflicts=(nautilus)

    cd nautilus
    make DESTDIR="${pkgdir}" install
    make DESTDIR="${pkgdir}" -C libnautilus-extension uninstall
    make DESTDIR="${pkgdir}" -C docs/reference/libnautilus-extension uninstall

    # Ubuntu specific stuff
    install -dm755 "${pkgdir}/usr/share/applications/"
    install -m644 \
        "${srcdir}/debian/nautilus-home.desktop" \
        "${srcdir}/debian/mount-archive.desktop" \
        "${srcdir}/debian/nautilus-folder-handler.desktop" \
        "${pkgdir}/usr/share/applications/"

    # Make Unity happy
    cp "${pkgdir}"/usr/share/applications/{org.gnome.Nautilus,nautilus}.desktop
    sed -i 's/NoDisplay=true/OnlyShowIn=Unity;/' \
        "${pkgdir}"/usr/share/applications/org.gnome.Nautilus.desktop
    sed -i 's/\[Desktop Entry\]/\0\nNotShowIn=Unity;/' \
        "${pkgdir}"/usr/share/applications/nautilus.desktop
}

package_libnautilus-extension-ubuntu() {
    pkgdesc="Library for extending the ${pkgdesc}"
    depends=(gtk3)
    provides=("libnautilus-extension=${pkgver}")
    conflicts=(libnautilus-extension)

    cd nautilus
    make DESTDIR="$pkgdir" -C libnautilus-extension install
    make DESTDIR="$pkgdir" -C docs/reference/libnautilus-extension install
}
