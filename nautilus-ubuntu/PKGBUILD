# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgbase%-*}, repo=utopic
# vercheck-archlinux: name=${pkgbase%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgbase%-*}, majorver=3.12
# vercheck-ppa: name=${pkgbase%-*}, url=ppa:gnome3-team/gnome3-staging

pkgbase=nautilus-ubuntu
pkgname=('nautilus-ubuntu' 'libnautilus-extension-ubuntu')
_ppa_rel=0ubuntu1~trusty2
pkgver=3.12.2
pkgrel=1
pkgdesc="GNOME and Unity file manager"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libexif' 'gnome-desktop' 'exempi' 'gvfs' 'desktop-file-utils' 'gnome-icon-theme' 'dconf' 'libtracker-sparql' 'libnotify' 'nautilus-sendto' 'libunity' 'libzeitgeist')
makedepends=('intltool' 'gobject-introspection' 'python')
url="http://www.gnome.org"
options=('!emptydirs')
install=nautilus.install
source=("http://ftp.gnome.org/pub/gnome/sources/nautilus/${pkgver%.*}/nautilus-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3-staging/ubuntu/pool/main/n/nautilus/nautilus_${pkgver}-${_ppa_rel}.debian.tar.xz")
sha512sums=('59a80fef3cedff5393061aeead552eec80bbd920ae71b918bb21ca6fc59ba1c4e85c641885a12dde5208345b8659f9d4a6e1ddf865f4cb94afd5a3a339839c01'
            'deb4133045d00dad8a79cf8e405073c450a6bd69c3653afe67efacb967f1e89f5c3f46a135997a85df8de8c6bf485e1afdd7d9225ab6a1cb5f839792afbb4187')

prepare() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  # Apply Ubuntu's patches

  # Disable patches
    # Don't use Ubuntu help
      sed -i '/15_use-ubuntu-help.patch/d' "${srcdir}/debian/patches/series"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying ${i} ..."
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  sed -i '/gnome_bg_set_draw_background/d' libnautilus-private/nautilus-desktop-background.c
}

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --libexecdir=/usr/lib/nautilus \
    --disable-update-mimedb \
    --disable-packagekit \
    --disable-schemas-compile \
    --disable-appindicator \
    --enable-unity

  make
}

package_nautilus-ubuntu() {
  depends+=('libnautilus-extension-ubuntu')
  groups=('gnome')
  provides=("nautilus=${pkgver}")
  conflicts=('nautilus')

  cd "${srcdir}/nautilus-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  # Split libnautilus-extension
  cd ..
  mkdir -p n-e/usr/{lib,share}
  mv "${pkgdir}"/usr/include n-e/usr
  mv "${pkgdir}"/usr/lib/{girepository-1.0,pkgconfig} n-e/usr/lib
  mv "${pkgdir}"/usr/lib/libnautilus-extension.so* n-e/usr/lib
  mv "${pkgdir}"/usr/share/{gir-1.0,gtk-doc} n-e/usr/share

  # Ubuntu specific stuff
  install -dm755 "${pkgdir}/usr/share/pixmaps/"
  install -dm755 "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/debian/nautilus.xpm" "${pkgdir}/usr/share/pixmaps/"
  install -m644 "${srcdir}/debian/mount-archive.desktop" "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/debian/nautilus-folder-handler.desktop" "${pkgdir}/usr/share/applications/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}

package_libnautilus-extension-ubuntu() {
  pkgdesc="Library for extending the ${pkgdesc}"
  depends=('cairo' 'gtk3')
  provides=("libnautilus-extension=${pkgver}")
  conflicts=('libnautilus-extension')

  mv n-e/* "${pkgdir}/"
}

# vim:set ts=2 sw=2 et:
