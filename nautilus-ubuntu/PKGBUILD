# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgbase%-*}, repo=yakkety
# vercheck-archlinux: name=${pkgbase%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgbase%-*}, majorver=3.20
# vercheck-ppa: name=${pkgbase%-*}, url=ppa:gnome3-team/gnome3-staging

_use_ppa=true

pkgbase=nautilus-ubuntu
pkgname=(nautilus-ubuntu libnautilus-extension-ubuntu)
_ppa_rel=0ubuntu1~yakkety2
_ppa_ver=3.20.1
pkgver=3.20.1+17+gf84d431
pkgrel=1
pkgdesc="Default file manager for GNOME and Unity"
url="https://wiki.gnome.org/Apps/Nautilus"
arch=(i686 x86_64)
license=(GPL)
depends=(libexif gnome-desktop exempi gvfs dconf libtracker-sparql
         nautilus-sendto libunity libzeitgeist)
makedepends=(intltool gobject-introspection python packagekit python2 gnome-common git gtk-doc)
options=(!emptydirs)
_commit=f84d4319dcd5fcfa460a37dfdb78c93d188e2703
source=("git://git.gnome.org/nautilus#commit=${_commit}"
        'git://git.gnome.org/libgd')

if [[ "${_use_ppa}" == "true" ]]; then
    source+=("https://launchpad.net/~gnome3-team/+archive/ubuntu/gnome3-staging/+files/nautilus_${_ppa_ver:-${pkgver}}-${_ppa_rel}.debian.tar.xz")
else
    source+=("https://launchpad.net/ubuntu/+archive/primary/+files/nautilus_${_ubuntu_ver:-${pkgver}}-${_ubuntu_rel}.debian.tar.xz")
fi

sha512sums=('SKIP'
            'SKIP'
            '50023b8935f7f9938634385a8df632ad03e734f9b78abe9d46af4a7d5058be66007a36d68af8ecf118d58030df386a30049629e6362cb3c85eb719d56354b17a')

pkgver() {
    cd nautilus
    git describe --tags | sed 's/-/+/g'
}

prepare() {
    cd nautilus

    git submodule init
    git config --local submodule.libgd.url "${srcdir}/libgd"
    git submodule update

    # Fixup missing tag...
    git rev-parse --verify -q 3.20.1 || git tag 3.20.1 6824271e2915e43fb12928ca884afa0f4de6532b

    # Apply Ubuntu's patches

    # Disable patches
    sed -i -e '/15_use-ubuntu-help.patch/d' \
           -e '/08_clean_session_capplet.patch/d' \
           -e '/0001-mime-actions-use-format-argument.patch/d' \
           -e '/0003-Fix-white-desktop.patch/d' \
           ../debian/patches/series

    for i in $(grep -v '#' ../debian/patches/series); do
        msg "Applying ${i} ..."
        patch -p1 -i "../debian/patches/${i}"
    done

    sed -i '/gnome_bg_set_draw_background/d' libnautilus-private/nautilus-desktop-background.c

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd nautilus

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --libexecdir=/usr/lib/nautilus \
        --disable-update-mimedb \
        --disable-schemas-compile \
        --disable-selinux \
        --enable-gtk-doc \
        --enable-packagekit \
        --enable-introspection \
        --enable-tracker \
        --enable-unity

    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package_nautilus-ubuntu() {
    depends+=(libnautilus-extension-ubuntu)
    groups=(gnome unity)
    provides=("nautilus=${pkgver}")
    conflicts=(nautilus)

    cd nautilus
    make DESTDIR="${pkgdir}" install
    make DESTDIR="${pkgdir}" -C libnautilus-extension uninstall
    make DESTDIR="${pkgdir}" -C docs/reference/libnautilus-extension uninstall

    # Ubuntu specific stuff
    install -dm755 "${pkgdir}/usr/share/applications/"
    install -m644 \
        "${srcdir}/debian/nautilus-home.desktop" \
        "${srcdir}/debian/mount-archive.desktop" \
        "${srcdir}/debian/nautilus-folder-handler.desktop" \
        "${pkgdir}/usr/share/applications/"

    # Make Unity happy
    cp "${pkgdir}"/usr/share/applications/{org.gnome.Nautilus,nautilus}.desktop
    sed -i 's/NoDisplay=true/OnlyShowIn=Unity;/' \
        "${pkgdir}"/usr/share/applications/org.gnome.Nautilus.desktop
    sed -i 's/\[Desktop Entry\]/\0\nNotShowIn=Unity;/' \
        "${pkgdir}"/usr/share/applications/nautilus.desktop
}

package_libnautilus-extension-ubuntu() {
    pkgdesc="Library for extending the ${pkgdesc}"
    depends=(gtk3)
    provides=("libnautilus-extension=${pkgver}")
    conflicts=(libnautilus-extension)

    cd nautilus
    make DESTDIR="$pkgdir" -C libnautilus-extension install
    make DESTDIR="$pkgdir" -C docs/reference/libnautilus-extension install
}
