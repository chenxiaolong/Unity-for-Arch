# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=libunity-webapps
_ubuntu_rel=0ubuntu3.1
_actual_ver=2.4.1
pkgver=${_actual_ver}.${_ubuntu_rel}
pkgrel=100
pkgdesc="WebApps: Library for the integration with the Unity desktop"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity-webapps"
license=('LGPL')
groups=('unity')
depends=('geoclue' 'glib2' 'hicolor-icon-theme' 'indicator-messages' 'json-glib' 'libdbusmenu-gtk3' 'libindicate' 'libindicate-gtk3' 'libnotify' 'libsoup' 'libunity' 'libwnck3' 'packagekit' 'polkit' 'sqlite' 'telepathy-glib')
makedepends=('gobject-introspection')
options=('!libtool')
install=libunity-webapps.install
source=("https://launchpad.net/libunity-webapps/trunk/${_actual_ver}/+download/unity_webapps-${_actual_ver}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/libunity-webapps_${_actual_ver}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('25e9024bd1e4109b284be414b3c5d010b69f359fcc4a7243d285b8e247ad4eb8bcd980d70af68436d66afafad9acfb4d5983b6dd0fa2f184fe7391b9d5254f5c'
            '7013d07728d8722893cbc166293b66ec90b1b45f2c4bad826375ff81814ebe00b305f5653bf55833a1226896e380ae11e218cf0f65480725a8c0e4bdfcbd634a')

build() {
  cd "${srcdir}/unity_webapps-${_actual_ver}"

  # Apply Ubuntu's patches
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  ./configure --prefix=/usr --libexecdir=/usr/lib/libunity-webapps --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/unity_webapps-${_actual_ver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
