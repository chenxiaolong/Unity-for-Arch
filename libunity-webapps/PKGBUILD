# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=libunity-webapps
_actual_ver=2.5.0
_extra_ver=~daily13.03.18
_translations=20130418
pkgver=${_actual_ver}${_extra_ver/\~/.}
pkgrel=103
pkgdesc="WebApps: Library for the integration with the Unity desktop"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity-webapps"
license=('LGPL')
groups=('unity')
depends=('geoclue' 'glib2' 'hicolor-icon-theme' 'indicator-messages' 'json-glib' 'libdbusmenu-gtk3' 'libnotify' 'libsoup' 'libunity' 'libwnck3' 'packagekit' 'polkit' 'sqlite' 'telepathy-glib')
makedepends=('gobject-introspection' 'gtk-doc' 'intltool')
checkdepends=('dbus-test-runner' 'xorg-server-xvfb')
options=('!libtool')
install=libunity-webapps.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libunity-webapps_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-libunity-webapps.tar.gz")
sha512sums=('2d0c75339c70d5ce238f65453eec994e25b272688894dfacef7be310b86d4f3ff4a5bc13371a0b2618c81b5e37060b93755ce3ab15ee1766375677d49499ed8d'
            '9cac4fc4333dd55ad0cad6a89686e4c240abbd9b41806583272826bfcaec53f4f51faf741c8bb532cf3decc850f38e56f4241e4349d662b1f7bfe9c937869285')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po "${srcdir}"/unity_webapps/*.po; do
    FILE=$(sed -n "s|.*/unity_webapps-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --libexecdir=/usr/lib/libunity-webapps --disable-static
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
