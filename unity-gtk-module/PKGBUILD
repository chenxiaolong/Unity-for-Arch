# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=utopic

pkgname=unity-gtk-module
_actual_ver=0.0.0
_extra_ver=+15.04.20150118
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
epoch=2
pkgdesc="Unity GTK+ Module"
arch=(i686 x86_64)
url="https://launchpad.net/unity-gtk-module"
license=(GPL)
groups=(unity)
depends=(gtk2-ubuntu gtk3)
makedepends=(gtk-doc)
provides=(appmenu-gtk2 appmenu-gtk3)
replaces=(appmenu-gtk2 appmenu-gtk3)
conflicts=(appmenu-gtk2 appmenu-gtk3)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-gtk-module_${_actual_ver}${_extra_ver}.orig.tar.gz"
        80unity-gtk-module)
sha512sums=('a78582f94736184b9f19cbcc795265e38101fb2eeeb0c9089811886de87607c5ac4615c6b46b6dc65e6ede4c8c9c8caca6765ab273c11b99860c01c9b2683833'
            'e30db7e3e1aba0abf100e619c667bac16d0a25ee3d9c67f65af532ab1a512dce5966e3c94f5d938c378dc30d9ed766fc2c9fa62e96235f29d923be5bad7f3a45')

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  [ -d build-gtk2 ] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=2 --enable-gtk-doc \
               --disable-static
  make
  popd

  [ -d build-gtk3 ] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=3 --enable-gtk-doc \
               --disable-static
  make
  popd
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  pushd build-gtk2
  make DESTDIR="${pkgdir}/" install
  popd

  pushd build-gtk3
  make DESTDIR="${pkgdir}/" install
  popd

  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  install -m755  "${srcdir}/80unity-gtk-module" \
                 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  rm -rvf "${pkgdir}/usr/share/upstart/"
}
