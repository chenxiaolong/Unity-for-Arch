# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-session
_actual_ver=12.10.5
_extra_ver=daily13.05.06.1
_translations=20130419
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="Indicator showing session management, status and user switching"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-session"
license=('GPL')
depends=('libindicator3' 'libdbusmenu-gtk3' 'dconf')
makedepends=('intltool')
checkdepends=('dbus-test-runner' 'gtest-ubuntu' 'xorg-gtest' 'xorg-util-macros')
optdepends=('lightdm: support for lightdm')
groups=('unity')
options=('!libtool')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-session_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-indicator-session.tar.gz"
        '0001_Lock_Screen_Live_CD_Detection.patch'
        '0002_There_is_no_help.patch'
        '0003_Link_Against_pthread.patch'
        '0004_Lock_Screen_Guest_Session_Detection.patch')
sha512sums=('302d11ab9f8e8ea010810fc3f9b36e6a6ad96d0d8b1d1a59d7f4347be8b9e3637e5ae04b0a79870dccb5212fbfede38e13d770d60a8f8552385ad3f8e205b402'
            'd58470441812a2500bd709877f8b3dcb629e095e8612c413cc34ee8f6101f0c15b25605b1deba3741c8e241711c9bb1bb2169e0acae923f9750afee4717c183f'
            'f0e000995339e8a722f3a863fea61032ca9ac262c1760c1b647f76f9eae3dc081ae8025607936d094fc6a58738f6bcaaa4bc9ee55ed9439f0eb213507d729e5d'
            'f00ea3d3c00da05211e869fbde613807671e2faf712f832aa47100486b5991ed00e0d301f0e05c56090f37fd556663de87f912b7af5e762f7925d21d2719f99d'
            '73cd938c4dddca0de9ef09e2a90ff4f5d132b1668f682e1bffa4fe3faafcb8df93de1a88b5bb324a75ac3e3013b0b13934b704b92cef4860de120db84bae8f2e'
            '3b4a496f67d0020816184b816becdf8ca837aec35d1cd2dd4a2a4f11b4824f7d5aaca9fbeca87f2e00e66133376bfc9af6ba7a3334bcdaaed216ad7b5a03abf8')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p0 -i "${srcdir}/0001_Lock_Screen_Live_CD_Detection.patch"
  patch -p1 -i "${srcdir}/0002_There_is_no_help.patch"
  patch -p1 -i "${srcdir}/0003_Link_Against_pthread.patch"
  patch -p0 -i "${srcdir}/0004_Lock_Screen_Guest_Session_Detection.patch"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-static --disable-schemas-compile
  make
}

check() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/"

  env -u LD_PRELOAD make check
}

package() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/"

  make DESTDIR="${pkgdir}/" install

  rm -vf "${pkgdir}/test-service"
}

# vim:set ts=2 sw=2 et:
