# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-session
_actual_ver=12.10.5
_extra_ver=+14.04.20140410
pkgver=${_actual_ver}daily14.04.10
pkgrel=1
pkgdesc="Indicator showing session management, status and user switching"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-session"
license=('GPL')
depends=('dconf')
makedepends=('cmake' 'intltool' 'python2')
optdepends=('lightdm: support for lightdm')
groups=('unity')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-session_${_actual_ver}${_extra_ver}.orig.tar.gz"
        'indicator-session.service'
        '0002_There_is_no_help.patch')
sha512sums=('8578dba1874e16d720b867c5aaacea115545886a0f520b90bb636fabc8e5c569041005d3201187d4d9ab8381e7f250ab452cf2ce90f4280209e54d59a9a29d01'
            '540c76c0f68b028194bb1000dd61a0c24f2f16427e79d237b94b8c65fe5e3e16eb6ea645f0fd33d734113727e25b6eb7452cb72f72399c9e8d370f74abba7511'
            '5326ff4022fe86fa3162d1f46341f2f352d4859bd5024da81d5640465cd3b242abedf820c2589c1da05e2eec3fc2e7535a2e4f2f8489333cb001e279094a4a15')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0002_There_is_no_help.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -Denable_tests=OFF \
    -Denable_lcov=OFF

  make
}

package() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build"

  make DESTDIR="${pkgdir}/" install

  rm -vf "${pkgdir}/test-service"

  # Please fix CMake scripts, Ubuntu...
  rm -rv "${pkgdir}/usr/share/locale/LC_MESSAGES/"

  for i in ../po/*.po; do
    FILELANG=$(basename ${i})
    FILELANG=${FILELANG%.po}
    install -dm755 "${pkgdir}/usr/share/locale/${FILELANG}/LC_MESSAGES/"
    msgfmt -v ${i} -o \
      "${pkgdir}/usr/share/locale/${FILELANG}/LC_MESSAGES/indicator-session.mo"
  done

  install -dm755 "${pkgdir}/usr/share/dbus-1/services/"
  install -m644 "${srcdir}/indicator-session.service" \
                "${pkgdir}/usr/share/dbus-1/services/"

  rm -r "${pkgdir}/usr/share/upstart/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}

# vim:set ts=2 sw=2 et:
