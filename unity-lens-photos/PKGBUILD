# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=unity-lens-photos
_actual_ver=1.0
_extra_ver=+14.04.20140318
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=2
epoch=1
pkgdesc="Unity lens for browsing photos"
arch=(any)
url="https://launchpad.net/unity-lens-photos"
license=(GPL)
groups=(unity)
depends=(python-httplib2 python-gobject python-oauthlib dee libunity libsoup
         libsoup-gnome)
makedepends=(python python-distutils-extra)
options=(!emptydirs)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-lens-photos_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('8a61f8966dcec0aad69d6134bd67a211a6bc78bc81f997022b2950a556a89b019e9c1d28b5cb1840d85f513d7b8b105f2148903bb03b29647a4302330d4de61d')

package() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    python setup.py install --root="${pkgdir}/" --optimize=1

    # We don't have Ubuntu online accounts
    rm "${pkgdir}/usr/share/dbus-1/services/unity-scope-facebook.service"
    rm "${pkgdir}/usr/share/dbus-1/services/unity-scope-flickr.service"
    rm "${pkgdir}/usr/share/dbus-1/services/unity-scope-picasa.service"
    rm "${pkgdir}/usr/share/unity-scopes/facebook/unity_facebook_daemon.py"
    rm "${pkgdir}/usr/share/unity-scopes/flickr/unity_flickr_daemon.py"
    rm "${pkgdir}/usr/share/unity-scopes/picasa/unity_picasa_daemon.py"
    rm "${pkgdir}/usr/share/unity/scopes/photos/facebook.scope"
    rm "${pkgdir}/usr/share/unity/scopes/photos/flickr.scope"
    rm "${pkgdir}/usr/share/unity/scopes/photos/picasa.scope"

    # Use language packs
    rm -r "${pkgdir}/usr/share/locale/"
}
