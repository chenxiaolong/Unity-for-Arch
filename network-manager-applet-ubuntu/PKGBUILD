# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@archlinux.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>

pkgname=network-manager-applet-ubuntu
_debian_rel=1
_translations=20130418
pkgver=0.9.8.2
pkgrel=100
pkgdesc="GNOME frontends to NetWorkmanager"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/NetworkManager/"
license=('GPL')
depends=("networkmanager>=${pkgver%.*}" 'libgnome-keyring' 'polkit-gnome' 'gtk3' 'libnotify' 'gnome-icon-theme' 'mobile-broadband-provider-info' 'iso-codes' 'libappindicator3')
makedepends=('intltool' 'gnome-bluetooth' 'gobject-introspection')
optdepends=('gnome-bluetooth: for PAN/DUN support')
provides=("network-manager-applet=${pkgver}")
conflicts=('network-manager-applet')
options=('!libtool' '!emptydirs')
install=network-manager-applet.install
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgname%-*}/${pkgver%.*.*}/${pkgname%-*}-${pkgver}.tar.xz"
        "http://ftp.de.debian.org/debian/pool/main/n/network-manager-applet/network-manager-applet_${pkgver}-${_debian_rel}.debian.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-network-manager-applet.tar.gz")
sha512sums=('c2c8e264bdafc367ffd31f1c6ad3278cb2cdb042a532c260982803109601906e61be6b2ea9298e19b06646acf995a36875d18f9c3c2a0edfac61929a77f885c5'
            'b364cea4913e351720c879f3117c9cd97d123b2d6ecf021ed2d3391f0bc7ef6990b830c8dca25885ce39944e6ea51a1bc41ab2fc4995b78efd96d39f6fba2559'
            '3c9a0c6b751ad3d9c5b83ceb3d76eda24f687dbedbbbc24b02c1ce57ba84cebc821e9dddc46b18a5c521d54160855c2ad8b5b8c5e1bc2679d08085ffb0ab48a3')

prepare() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  # Apply Ubuntu's patches
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying ${i} ..."
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/nm-applet-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --disable-static \
    --disable-maintainer-mode \
    --disable-migration \
    --enable-indicator \
    --disable-more-warnings

  make -j1
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
