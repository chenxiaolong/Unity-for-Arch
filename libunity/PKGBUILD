# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=libunity
_ubuntu_rel=0ubuntu1
pkgver=6.10.0.${_ubuntu_rel}
pkgrel=100
pkgdesc="A library for instrumenting and integrating with all aspects of the Unity shell"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity"
license=('LGPL')
depends=('libgee' 'dee' 'libdbusmenu')
makedepends=('vala' 'python2' 'gobject-introspection')
groups=('unity')
options=('!libtool')
source=("http://launchpad.net/${pkgname}/6.0/${pkgver%.*}/+download/${pkgname}-${pkgver%.*}.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff.gz")
sha512sums=('6e8117216a17bb89e8907bbe6565a50640e77e87bdb947c50a53bac693528e39d35f6271550fc50016bf8476df2a542e1f7af3fe0a1ecafb339e51de99d6c7bd'
            '5bde9a0a505857e530ade2793427477212a2dfdd6a30aedc70adc29c53c62ae47f6e087ac45d74fddac20263c934d99fc336cd5edbbf6b359bfcd04ccf6d53aa')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  # Apply Ubuntu patches
  patch -Np1 -i "${srcdir}/${pkgname}_${pkgver%.*}-${_ubuntu_rel}.diff"

  for i in $(cat 'debian/patches/series' | grep -v '#'); do
    patch -Np1 -i "debian/patches/${i}"
  done

  autoreconf -vfi
  ./configure --prefix=/usr --disable-static PYTHON=python2
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
