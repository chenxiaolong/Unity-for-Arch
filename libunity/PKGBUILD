# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=libunity
_actual_ver=7.1.4
_extra_ver=+15.10.20151002
_ubuntu_rel=0ubuntu2
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=3
pkgdesc="A library for instrumenting and integrating with all aspects of the Unity shell"
arch=(i686 x86_64)
url="https://launchpad.net/libunity"
license=(LGPL)
depends=(dee-ubuntu libdbusmenu-glib gtk3)
makedepends=(intltool vala python2 gobject-introspection)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libunity_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/libunity_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz"
        0001_autoconf.patch)
sha512sums=('2e3177e1471d51919fbf660d568bac8c39fb81d34f5996530fadeb56d43c75097aacf0538e70ca5ef05a2a94742ae7a4783576efe34abe7470c8fc32c8559838'
            '2a677979b5b7b617b1731bad5d1bbec8300dd6dbb27ea6c83b6da12d94a4a2a1706f8e7e473c09f92b5b6c1be8f6a7ea0f5455afb8703887fd438c7103e06bb3'
            '1570c01f4c8ac53bc7806fad78691844a12625b7142270b5a215d203618acc76ba258c4aa969a120186ba9224b726f20a2a9efad05940ffdaa258be49844fa4b')

prepare() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    find -type f -name '*.py' -exec sed -i 's|^\(#!.*python$\)|\12|g' {} \+

    patch -p1 -i ../0001_autoconf.patch

    # Apply Ubuntu patches
    patch -p1 -i ../${pkgname}_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff

    for i in $(grep -v '#' debian/patches/series); do
        msg "Applying ${i} ..."
        patch -p1 -i "debian/patches/${i}"
    done
}

build() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    intltoolize -f
    autoreconf -vfi
    ./configure --prefix=/usr --disable-static --enable-headless-tests PYTHON=python2
    make -j1
}

package() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    make DESTDIR="${pkgdir}/" install
}
