# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=libappindicator
pkgname=('libappindicator' 'libappindicator3')
_actual_ver=12.10.1
_extra_ver=daily13.04.15
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="A library to allow applications to export a menu into the Unity Menu bar"
arch=('i686' 'x86_64')
url="https://launchpad.net/libappindicator"
license=('LGPL')
makedepends=('dbus-glib' 'libindicator' 'libindicator3' 'libdbusmenu-gtk2' 'libdbusmenu-gtk3' 'gobject-introspection' 'vala' 'gtk-sharp-2' 'pygtk' 'perl-xml-libxml' 'gtk-doc')
checkdepends=('dbus-test-runner' 'xorg-server-xvfb')
groups=('unity')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_g_type_init.patch')
sha512sums=('ce83e53c51a6edef22b4b7806e3f755a4218f009afcdb76b2cbe6aa580962ce34488c2457a4ff7f816032f7284d59fcfd232a8493960b91ae788b5e16ec4f82e'
            '9d58a76f7e9896573a39719596dd4d535a2293f970b6ed0bb156406f4abe715dbbade4b9beb7f3e6b7cf85d26a727750e8fc4e2f0b3525f0ac269cc59f8c30bf')

prepare() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_g_type_init.patch"

  # nunit renamed to mono-nunit in /usr/lib/pkgconfig/mono-nunit.pc
  sed -i 's/nunit >= 2\.4\.7/mono-nunit >= 2.4.7/' configure.ac

  # The /usr/lib/cli location for Mono bindings is specific to Ubuntu
  sed -i 's|assemblydir = $(libdir)/cli/appindicator-sharp-0.1|assemblydir = $(libdir)/appindicator-sharp-0.1|' bindings/mono/Makefile.am
  sed -i 's|assemblies_dir=${libdir}/cli/appindicator-sharp-0.1|assemblies_dir=${libdir}/appindicator-sharp-0.1|' bindings/mono/appindicator-sharp-0.1.pc.in

  sed -i 's/2\.35\.4/2.34.3/g' configure.ac
}

build() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error"

  gtkdocize
  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --with-gtk=2 --disable-static PYTHON=python2
  export MAKEFLAGS="-j1"
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --with-gtk=3 --disable-static PYTHON=python2
  make
  popd
}

check() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk2"
  make check

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk3"
  make check
}

package_libappindicator() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('libindicator' 'libdbusmenu-gtk2')
  conflicts=('indicator-application<0.2.91')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk2"

  export MAKEFLAGS="-j1"
  make DESTDIR="${pkgdir}/" install
}

package_libappindicator3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=('libindicator3' 'libdbusmenu-gtk3')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk3"

  make -C src DESTDIR="${pkgdir}/" install
  make -C bindings/vala DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
