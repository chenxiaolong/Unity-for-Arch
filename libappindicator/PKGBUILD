# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=libappindicator
pkgname=('libappindicator' 'libappindicator3')
_actual_ver=12.10.1
_extra_ver=+13.10.20130920
pkgver=${_actual_ver}daily13.09.20
pkgrel=101
pkgdesc="A library to allow applications to export a menu into the Unity Menu bar"
arch=('i686' 'x86_64')
url="https://launchpad.net/libappindicator"
license=('LGPL')
makedepends=('dbus-glib' 'libindicator' 'libindicator3' 'libdbusmenu-gtk2' 'libdbusmenu-gtk3' 'gobject-introspection' 'vala' 'gtk-sharp-2' 'pygtk' 'perl-xml-libxml' 'gtk-doc')
groups=('unity')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_Timeout.patch')
sha512sums=('7a93a94dfac6f468e79e0121ca65bdec2fe47e278abf5f097ad856c2ce38314b9d14b7a565bc36ea7fd0c495a95dd754896adeb39c08ff4ebd6006c2c1dbe08d'
            'b14f77a93473d7ef92a76aff0185010858f130dfe8c140b78876c6cc3e835508e66063139fc4adc469d81b6b9bec0c04fab77a990fef67df00ed95c4b905046d')

prepare() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_Timeout.patch"

  # nunit renamed to mono-nunit in /usr/lib/pkgconfig/mono-nunit.pc
  sed -i 's/nunit >= 2\.4\.7/mono-nunit >= 2.4.7/' configure.ac

  # The /usr/lib/cli location for Mono bindings is specific to Ubuntu
  sed -i 's|assemblydir = $(libdir)/cli/appindicator-sharp-0.1|assemblydir = $(libdir)/appindicator-sharp-0.1|' bindings/mono/Makefile.am
  sed -i 's|assemblies_dir=${libdir}/cli/appindicator-sharp-0.1|assemblies_dir=${libdir}/appindicator-sharp-0.1|' bindings/mono/appindicator-sharp-0.1.pc.in
}

build() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  gtkdocize
  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --with-gtk=2 --disable-static PYTHON=python2
  export MAKEFLAGS="-j1"
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --with-gtk=3 --disable-static PYTHON=python2
  make
  popd
}

package_libappindicator() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('libindicator' 'libdbusmenu-gtk2')
  conflicts=('indicator-application<0.2.91')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk2"

  export MAKEFLAGS="-j1"
  make DESTDIR="${pkgdir}/" install
}

package_libappindicator3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=('libindicator3' 'libdbusmenu-gtk3')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk3"

  make -C src DESTDIR="${pkgdir}/" install
  make -C bindings/vala DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
