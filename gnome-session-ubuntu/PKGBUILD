# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname%-*}, repo=yakkety
# vercheck-archlinux: name=${pkgname%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgname%-*}, majorver=3.22
# vercheck-ppa: name=${pkgname%-*}, url=ppa:gnome3-team/gnome3-staging

pkgname=gnome-session-ubuntu
_use_ppa=false
#_ppa_rel=2ubuntu1~xenial1
_ubuntu_rel=1ubuntu7
_ubuntu_ver=3.20.2
pkgver=3.22.1
pkgrel=2
pkgdesc="The GNOME Session Handler"
url="https://git.gnome.org/browse/gnome-session"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(systemd dconf gsettings-desktop-schemas gtk3 gnome-desktop
         json-glib libgl libsm libxtst systemd polkit-gnome)
# TODO: Temporary until systemd 232 is released
depends+=('systemd>231')
makedepends=(intltool mesa gtk-doc xtrans git gnome-common python)
groups=(gnome unity)
provides=("gnome-session=${pkgver}")
conflicts=(gnome-session systemd-unity)
_commit=09f44cc50eafa047856d483ee18ebae99f8b8352  # tags/3.22.1^0
source=("git://git.gnome.org/gnome-session#commit=${_commit}"
        50_ubuntu_sessions.patch
        polkit-gnome-authentication-agent-1-unity.desktop)

if [[ "${_use_ppa}" == "true" ]]; then
    source+=("https://launchpad.net/~gnome3-team/+archive/ubuntu/gnome3-staging/+files/gnome-session_${_ppa_ver:-${pkgver}}-${_ppa_rel}.debian.tar.xz")
else
    source+=("https://launchpad.net/ubuntu/+archive/primary/+files/gnome-session_${_ubuntu_ver:-${pkgver}}-${_ubuntu_rel}.debian.tar.xz")
fi

sha512sums=('SKIP'
            '0ddbeff2f9aafc06257f8fbad77cee7d22c73daaa63fa1d7e061a10ed7533bc5fe3306979d949e27d0e7bce9450ee6269e7bd7920e4233944dff78884e423429'
            '34e32d1c3fd7c417b5dc70737b9dff9c9b0b660a6c99cb8c29e944818df0cb643fcac01c6c5252849bb8595a348aaf4d2a24985560c6b8a9653544e146e5b4af'
            '2a9e4265c659d8806f4c743984abd304bcf4fafefbcd9d1f6d8487e2c9b682b0dff943b17e5f2a97376971b97ec66f8291fa8ab752e0419bb54032177c3812fb')

pkgver() {
    cd "${pkgname%-*}"
    git describe --tags | sed 's/-/+/g'
}

prepare() {
    cd "${pkgname%-*}"

    sed -i '/53_add_sessionmigration.patch/d' ../debian/patches/series

    # Already in 3.22
    sed -i '/allow_overriding_qt_platformtheme.patch/d' ../debian/patches/series
    sed -i '/dont_override_IM_variables.patch/d' ../debian/patches/series

    # If you want this, install gnome-session-properties from the AUR
    sed -i '/13_display_session_properties.patch/d' ../debian/patches/series
    sed -i '/revert_remove_gnome_session_properties.patch/d' ../debian/patches/series
    sed -i '/51_remove_session_saving_from_gui.patch/d' ../debian/patches/series

    for i in $(grep -v '#' ../debian/patches/series); do
        msg "Applying ${i} ..."
        case "${i}" in
        50_ubuntu_sessions.patch)
            patch -p1 -i "../${i}"
            ;;
        *)
            patch -p1 -i "../debian/patches/${i}"
            ;;
        esac
    done

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${pkgname%-*}"

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/gnome-session \
        --disable-schemas-compile \
        --enable-systemd \
        --disable-gconf

    make
}

package() {
    cd "${pkgname%-*}"
    make DESTDIR="${pkgdir}" install

    # polkit
    install -dm755 "${pkgdir}/etc/xdg/autostart/"
    install -m644 "${srcdir}/polkit-gnome-authentication-agent-1-unity.desktop" \
        "${pkgdir}/etc/xdg/autostart/"

    # LightDM
    install -dm755 "${pkgdir}/usr/share/lightdm/lightdm.conf.d/"
    install -m644 "${srcdir}/debian/data/50-ubuntu.conf" \
        "${pkgdir}/usr/share/lightdm/lightdm.conf.d/"

    # Upstart
    install -Dm644 "${srcdir}/debian/gnome-session-bin.user-session.upstart" \
        "${pkgdir}/usr/share/upstart/sessions/gnome-session.conf"

    install -dm755 "${pkgdir}/usr/share/upstart/systemd-session/upstart/"
    install -m644 "${srcdir}/debian/data/gnome-session.override" \
        "${pkgdir}/usr/share/upstart/systemd-session/upstart/"

    # systemd
    install -dm755 "${pkgdir}/usr/lib/systemd/user/"
    install -m644 \
        "${srcdir}/debian/data/gnome-session.service" \
        "${srcdir}/debian/data/ubuntu-session.target" \
        "${pkgdir}/usr/lib/systemd/user/"

    install -dm755 "${pkgdir}/usr/lib/gnome-session/"
    install -m755 "${srcdir}/debian/data/run-systemd-session" \
        "${pkgdir}/usr/lib/gnome-session/"
}
