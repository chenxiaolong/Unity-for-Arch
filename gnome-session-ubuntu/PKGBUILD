# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-session-ubuntu
_ppa_ver=3.8.0
_ppa_rel=0ubuntu1~raring1
_translations=20130418
pkgver=3.8.1
pkgrel=101
pkgdesc="The GNOME Session Handler"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('systemd' 'dconf' 'gconf' 'gsettings-desktop-schemas' 'gtk3' 'gnome-desktop' 'hicolor-icon-theme' 'json-glib' 'libgl' 'libsm' 'libxtst' 'polkit-gnome' 'upower' 'gnome-settings-daemon-compat')
makedepends=('gnome-common' 'gtk-doc' 'intltool' 'mesa' 'xtrans' 'patchutils')
groups=('gnome')
provides=("gnome-session=${pkgver}")
conflicts=('gnome-session')
options=('!emptydirs')
install=gnome-session.install
url="http://www.gnome.org"
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3/ubuntu/pool/main/g/gnome-session/gnome-session_${_ppa_ver}-${_ppa_rel}.debian.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-gnome-session.tar.gz"
        '55gnome-session_gnomerc'
        '50_ubuntu_sessions.patch'
        '52_xdg_current_desktop.patch'
        'timeout.patch')
sha512sums=('95a218c63bd6c9d0d04fc094923a245485361d995dc5753c4e8573c269c85ad3d91de42e67eb469f33ca6df1ae6636de42b109696967e4640ffe89f11f8d9d81'
            'd37055538bacff741865a47916b551cffed6f28293cd01de6c3a75660382ba1d33c049c10f7a5a51fcfc861bf910f535ef0e96dd1e80211c8b76dc196bc46f3d'
            'f65eb708d47ab132068834ff150219702b871f8ebbb5c308e52d6fbc5aa49fbe2e10dbf32e54674ffc2ee853d0248f01118f6692cba47b1d7b787abfbdbda75f'
            '9246e9bcb25493d169064a11cd0609c565e7173464fb594c8feee281c196127af604a8f2b3b8d07edb82ea35bfd3bc5b586719676ec868ee25c102039cb5415a'
            'a03bad59c6aff580278f8916a04b50d99d1b9895483759baf2f04da7f0cc172e37e3be80725528bd2b0105869a2a26f664aee85dae655432ac863ae5a69146de'
            'c0e72c98312913ea8998de748b5eed4c553fae97cf4ac1900ed69e1e35676ac91f649a7ccad13cca2737cb4564ad8be25750d17c2ede18481be53037a8ae6e3c'
            '81d28b8f345c3a16dbb443ee8f195f802b572941aeac2dafaf6ea6b231ccb89a280aa855d09744dfc7ef2ad794a5e7b0002b54b14465865441367055514bcb70')

prepare() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  # Increase timeout, for slow machines
  patch -p1 -i "${srcdir}/timeout.patch"

  # Disable patches
    # We don't have GNOME fallback stuff anymore
      sed -i '/00_revert_remove_fallback_session.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/02_fallback_desktop.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/03_fallback_desktop_makefile.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/git_revert_isRunnableHelper.patch/d' "${srcdir}/debian/patches/series"
    # gnome-wm uses Debian's alternatives system (Fedora has it too, but other
    # packages need to use it)
      sed -i '/01_gnome-wm.patch/d' "${srcdir}/debian/patches/series"
    # We're not Ubuntu, do not hide stuff
      sed -i '/20_hide_nodisplay.patch/d' "${srcdir}/debian/patches/series"
    # Fedora does not have sessionmigration
      sed -i '/53_add_sessionmigration.patch/d' "${srcdir}/debian/patches/series"

  # Fix patches
    # Needed because 01_gnome-wm.patch is disabled
      sed -i 's/gnome-wm/metacity/g' "${srcdir}/debian/patches/50_ubuntu_sessions.patch"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying patch ${i} ..."
    if [ "x${i}" = "x50_ubuntu_sessions.patch" ] || \
       [ "x${i}" = "x52_xdg_current_desktop.patch" ]; then
      patch -p1 -i "${srcdir}/${i}"
    else
      patch -p1 -i "${srcdir}/debian/patches/${i}"
    fi
  done

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/gnome-session-3.0-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/gnome-session \
    --disable-schemas-compile \
    --enable-systemd \
    --enable-ipv6 # From debian/rules

  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Install Ubuntu files
  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  #install -m755 "${srcdir}/debian/55gnome-session_gnomerc" "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  # Install my version of the script (more platform-agnostic)
  install -m755 "${srcdir}/55gnome-session_gnomerc" "${pkgdir}/etc/X11/xinit/xinitrc.d/"

  # symlink session files so that KDM can find them
  install -dm755 "${pkgdir}/usr/share/apps/kdm/sessions/"
  ln -s /usr/share/xsessions/ubuntu.desktop "${pkgdir}/usr/share/apps/kdm/sessions/"
  ln -s /usr/share/xsessions/ubuntu-2d.desktop "${pkgdir}/usr/share/apps/kdm/sessions/"
}

# vim:set ts=2 sw=2 et:
