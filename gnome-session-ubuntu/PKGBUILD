# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: thn81 <root@scrat>

pkgname=gnome-session-ubuntu
_ppa_ver=3.7.92
_ppa_rel=0ubuntu1~raring1
pkgver=3.8.0
pkgrel=101
pkgdesc="The GNOME Session Handler"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('systemd' 'dconf' 'gconf' 'gsettings-desktop-schemas' 'gtk3' 'gnome-desktop' 'hicolor-icon-theme' 'json-glib' 'libgl' 'libsm' 'libxtst' 'polkit-gnome' 'upower')
makedepends=('gnome-common' 'gtk-doc' 'intltool' 'mesa' 'xtrans' 'patchutils')
groups=('gnome')
provides=("gnome-session=${pkgver}")
conflicts=('gnome-session')
options=('!emptydirs')
install=gnome-session.install
url="http://www.gnome.org"
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3/ubuntu/pool/main/g/gnome-session/gnome-session_${_ppa_ver}-${_ppa_rel}.debian.tar.gz"
        '55gnome-session_gnomerc'
        '50_ubuntu_sessions.patch'
        '52_xdg_current_desktop.patch'
        'timeout.patch')
sha512sums=('81fa89b8f2d232a31f59250e40a183e5fc1bd29fd56cf14dff0f9ba5cd6e891fcdec99fa616556e43ba79e0d5b0a9f9ef838f9383c7f847bc975a4f5cd9c4be9'
            '3ceed5ea15852905cf636667ee0315e90c690d7f27cc889036a0572868f0e05c350f409903c1e2a42326fd98d839ff65655182f3443bb4d450aec8f77d2c50cc'
            '9246e9bcb25493d169064a11cd0609c565e7173464fb594c8feee281c196127af604a8f2b3b8d07edb82ea35bfd3bc5b586719676ec868ee25c102039cb5415a'
            'c5289fabe2203d6d9127dfd7f0a6957cd52b23944b22331dec2da543426afe32eee87b34fb05fff48a3474ea003e746d317b4f48294e6000db872f7d2dc2690c'
            'c0e72c98312913ea8998de748b5eed4c553fae97cf4ac1900ed69e1e35676ac91f649a7ccad13cca2737cb4564ad8be25750d17c2ede18481be53037a8ae6e3c'
            '81d28b8f345c3a16dbb443ee8f195f802b572941aeac2dafaf6ea6b231ccb89a280aa855d09744dfc7ef2ad794a5e7b0002b54b14465865441367055514bcb70')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  # Increase timeout, for slow machines
  patch -p1 -i "${srcdir}/timeout.patch"

  # Disable patches
    # We don't have GNOME fallback stuff anymore
      sed -i '/00_revert_remove_fallback_session.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/02_fallback_desktop.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/03_fallback_desktop_makefile.patch/d' "${srcdir}/debian/patches/series"
      sed -i '/git_revert_isRunnableHelper.patch/d' "${srcdir}/debian/patches/series"
    # gnome-wm uses Debian's alternatives system (Fedora has it too, but other
    # packages need to use it)
      sed -i '/01_gnome-wm.patch/d' "${srcdir}/debian/patches/series"
    # We're not Ubuntu, do not hide stuff
      sed -i '/20_hide_nodisplay.patch/d' "${srcdir}/debian/patches/series"
    # Fedora does not have sessionmigration
      sed -i '/53_add_sessionmigration.patch/d' "${srcdir}/debian/patches/series"

  # Fix patches
    # Needed because 01_gnome-wm.patch is disabled
      sed -i 's/gnome-wm/metacity/g' "${srcdir}/debian/patches/50_ubuntu_sessions.patch"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying patch ${i} ..."
    if [ "x${i}" = "x50_ubuntu_sessions.patch" ] || \
       [ "x${i}" = "x52_xdg_current_desktop.patch" ]; then
      patch -p1 -i "${srcdir}/${i}"
    else
      patch -p1 -i "${srcdir}/debian/patches/${i}"
    fi
  done

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/gnome-session \
    --disable-schemas-compile \
    --enable-systemd \
    --enable-ipv6 # From debian/rules

  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Install Ubuntu files
  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  #install -m755 "${srcdir}/debian/55gnome-session_gnomerc" "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  # Install my version of the script (more platform-agnostic)
  install -m755 "${srcdir}/55gnome-session_gnomerc" "${pkgdir}/etc/X11/xinit/xinitrc.d/"

  # symlink session files so that KDM can find them
  install -dm755 "${pkgdir}/usr/share/apps/kdm/sessions/"
  ln -s /usr/share/xsessions/ubuntu.desktop "${pkgdir}/usr/share/apps/kdm/sessions/"
  ln -s /usr/share/xsessions/ubuntu-2d.desktop "${pkgdir}/usr/share/apps/kdm/sessions/"
}

# vim:set ts=2 sw=2 et:
