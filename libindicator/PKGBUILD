# Maintainer: Xiao-long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=libindicator
pkgname=('libindicator' 'libindicator3')
_actual_ver=12.10.2
_extra_ver=+13.10.20130702
pkgver=${_actual_ver}daily13.07.02
pkgrel=100
pkgdesc="A set of symbols and convience functions that all indicators would like to use"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicator"
license=('GPL')
makedepends=('gtk2' 'gtk3' 'glib2-ubuntu' 'ido')
checkdepends=('dbus-test-runner' 'xorg-server-xvfb')
options=('!libtool')
groups=('unity')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgbase}_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('c6e552e23a55aa228ff81bcafc31c452676a00279a46be1f699a18c4f116827e61d6752b8197cef9591626618b51fc97c74ccf3e0b60e67fd4d1acc349680b5d')

build() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error=deprecated-declarations"

  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --with-gtk=2 --disable-static
  export MAKEFLAGS="-j1"
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --with-gtk=3 --disable-static
  make
  popd
}

# Ubuntu/Debian's dh_auto_test is not being run for this package (probably a
# packaging mistake)
check() {
  # test-indicator-ng fails under GTK 3
  #cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk3"
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk2"
  make check || :
}

package_libindicator() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('gtk2' 'glib2-ubuntu')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk2"

  export MAKEFLAGS="-j1"
  make DESTDIR="${pkgdir}/" install
}

package_libindicator3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=('gtk3' 'glib2-ubuntu' 'ido')
  options=(${options[@]} '!emptydirs')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk3"

  export MAKEFLAGS="-j1"
  make -C libindicator DESTDIR="${pkgdir}/" install
  make -C tools DESTDIR="${pkgdir}/" install

  # Identical in both packages, but Ubuntu puts it in the GTK 2 package
    # Debug
      rm -vf  "${pkgdir}/usr/share/libindicator/80indicator-debugging"
}

# vim:set ts=2 sw=2 et:
