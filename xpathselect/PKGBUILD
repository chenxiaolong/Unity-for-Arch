# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=xpathselect
_actual_ver=1.3
_extra_ver=daily13.06.05.1
pkgver=${_actual_ver}${_extra_ver}
pkgrel=2
pkgdesc="Select objects in an object tree using XPath queries"
arch=('i686' 'x86_64')
url="https://launchpad.net/xpathselect"
license=('GPL')
groups=('unity')
makedepends=('boost' 'cmake')
checkdepends=('gtest-ubuntu')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/xpathselect_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_Disable_Tests.patch')
sha512sums=('1b1dadd12cc68dd37110ca4a3e659306dced792329a475e65fe5b730ee15cf912bddd733cd0bb3b6c2d3cd8dc8fd81089a6bebaadd6254e1cc34468db14f9906'
            '3bdee2bd7f89903ece07bb81848365d5e493895b72a58a874c285b51f795884b6d191bef8d6910479ba39b091bce1828575b8c81f20f113a56c2fc3683c94d15')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  if [ ! -d /usr/src/gtest/ ]; then
    patch -p1 -i "${srcdir}/0001_Disable_Tests.patch"
  fi
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/build"
  make DESTDIR="${pkgdir}/" install
}
