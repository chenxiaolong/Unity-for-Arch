# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=xpathselect
_actual_ver=1.4
_extra_ver=+14.04.20140303
pkgver=${_actual_ver}daily14.03.03
pkgrel=1
pkgdesc="Select objects in an object tree using XPath queries"
arch=('i686' 'x86_64')
url="https://launchpad.net/xpathselect"
license=('GPL')
groups=('unity')
depends=('gcc-libs')
makedepends=('boost' 'cmake')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/xpathselect_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_Disable_Tests.patch')
sha512sums=('40981611ba2d951a0307a543ec2486d8f5e5f2c54534504f72d972a63709c60870aafec12d4ee8cce0bcd78ea0df5280aa92f181cfa253aff74e9594ce2e8b6f'
            '3bdee2bd7f89903ece07bb81848365d5e493895b72a58a874c285b51f795884b6d191bef8d6910479ba39b091bce1828575b8c81f20f113a56c2fc3683c94d15')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  if [ ! -d /usr/src/gtest/ ]; then
    patch -p1 -i "${srcdir}/0001_Disable_Tests.patch"
  fi
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/build"
  make DESTDIR="${pkgdir}/" install
}
