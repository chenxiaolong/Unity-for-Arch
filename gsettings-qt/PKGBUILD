# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial

pkgname=gsettings-qt
_actual_ver=0.1
_extra_ver=+16.04.20160111
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="Library to access GSettings from Qt"
arch=(i686 x86_64)
url="https://launchpad.net/gsettings-qt"
license=(LGPL)
groups=(unity)
depends=(qt5-base qt5-declarative)
makedepends=(qtchooser)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/gsettings-qt_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('92ae5378c513c10d16a7ca24a525f019654c7ecb1996ec697f311c93136aa83c96d95f3ea9bcececf35072e80671093ba56fc73922f95c826466c551367b3f9a')

prepare() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"
    sed -i -e 's|tests/tests.pro||g' \
           -e 's|tests/cpptest.pro||g' \
           gsettings-qt.pro
}

build() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"
    qmake-qt5 gsettings-qt.pro
    make
}

package() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"
    make INSTALL_ROOT="${pkgdir}/" install
}
