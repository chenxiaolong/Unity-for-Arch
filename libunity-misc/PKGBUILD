# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=libunity-misc
_actual_ver=4.0.5
_extra_ver=+14.04.20140115
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
epoch=1
pkgdesc="Misc. differently licensed stuff for Unity"
arch=(i686 x86_64)
url="https://launchpad.net/libunity-misc"
license=(LGPL)
depends=(cairo gtk3)
makedepends=(gtk-doc)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001_autotools.patch)
sha512sums=('2fc65db5320572cb5d5bcc326759a8e395887d3a408bb2b2823045ade8357d798b93162437878b59c69c8a69dc1055ba7f720b23cdeefc994d5335cbb7474006'
            'f1f7059f9eb90836383a3742cb4ff701cf1187209a6e7afc23b5efe9a70f20084d4a4d8d499469de79ed0679ead1d9ff45c7b5fc8207a3ba30e624eeadd3be86')

prepare() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    patch -p1 -i ../0001_autotools.patch
}

build() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    gtkdocize
    autoreconf -vfi
    ./configure --prefix=/usr --disable-static
    make
}

package() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    make DESTDIR="${pkgdir}/" install
}
