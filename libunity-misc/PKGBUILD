# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=libunity-misc
_actual_ver=4.0.5
_extra_ver=daily13.02.26
pkgver=${_actual_ver}${_extra_ver}
pkgrel=103
pkgdesc="Misc. differently licensed stuff for Unity"
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity-misc"
license=('LGPL')
depends=('gtk3')
makedepends=('gtk-doc')
groups=('unity')
options=(!libtool)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_autotools.patch')
sha512sums=('37d3a65c5f91e06cc3a601ee223c86a3aa631d48ca2d6f32816bedff48ad4a3714cb008f836a5fe27c8eab7407cfe1acf4c151139c1e429fd93413388c55daa2'
            'f1f7059f9eb90836383a3742cb4ff701cf1187209a6e7afc23b5efe9a70f20084d4a4d8d499469de79ed0679ead1d9ff45c7b5fc8207a3ba30e624eeadd3be86')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_autotools.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error"

  gtkdocize
  autoreconf -vfi
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
