# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Why are there three names?
# - gnome-control-center-signon (Ubuntu's packaging)
# - credentials-control-center (Source tarball)
# - online-accounts-gnome-control-center (Website)

# We'll use the same package names as Ubuntu (gnome-control-center-signon)

pkgname=gnome-control-center-signon
pkgver=0.0.19
pkgrel=100
pkgdesc="GNOME Control Center extension for single signon"
arch=('i686' 'x86_64')
url="https://launchpad.net/online-accounts-gnome-control-center"
license=('GPL')
groups=('unity')
depends=('dbus-glib' 'gobject-introspection' 'libaccounts-glib' 'gnome-control-center' 'libsignon-glib' 'signon' 'hicolor-icon-theme')
makedepends=('gnome-common' 'gnome-doc-utils' 'gtk-doc' 'itstool' 'vala' 'yelp-tools')
options=('!libtool')
install=gnome-control-center-signon.install
source=("https://launchpad.net/online-accounts-gnome-control-center/trunk/${pkgver}/+download/credentials-control-center-${pkgver}.tar.bz2")
sha512sums=('a9508109167846496d5498248983c6aad6bb9ff2ec7e2281b61799788cc72ef7bb1f808f7a1f2457192092dadd4118bfe6b301c9f1e60a23df676d4a9b207c40')

build() {
  cd "${srcdir}/credentials-control-center-${pkgver}"
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --enable-gtk-doc
  make -j1
}

package() {
  cd "${srcdir}/credentials-control-center-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
