# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=indicator-bluetooth
_actual_ver=0.0.6
_extra_ver=daily13.02.19
pkgver=${_actual_ver}.${_extra_ver}
pkgrel=101
pkgdesc="Bluetooth status indicator"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-bluetooth"
license=('GPL')
groups=('unity')
depends=('gnome-bluetooth' 'ido' 'libdbusmenu-gtk3' 'libindicator3')
makedepends=('gnome-common' 'intltool' 'vala')
# We don't want two bluetooth indicators
conflicts=('gnome-bluetooth-ubuntu')
options=('!libtool')
# Ubuntu finally using xz sources? It's a miracle!
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-bluetooth_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('1f5418104d6f3d74bb2003474347a8dc386abd37d2ae9ee86f7d290d1934eca4dbc5acce5d618f14cf98be6ef7ee67d0952b46af3ba2843ba7b127803cfcb2f0')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/indicator-bluetooth --disable-static
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
