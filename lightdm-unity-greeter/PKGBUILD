# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Taken from M0Rf30 morf3089 at gmail dot com

pkgname=lightdm-unity-greeter
_ubuntu_rel=0ubuntu1
pkgver=14.04.9
pkgrel=1
pkgdesc="The greeter (login screen) application for Unity. It is implemented as a LightDM greeter."
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-greeter"
license=('GPL3' 'LGPL3')
groups=('unity-extra')
depends=('cantarell-fonts' 'gnome-doc-utils' 'ido' 'libindicator3' 'lightdm-ubuntu' 'gnome-desktop' 'libcanberra')
makedepends=('gnome-common' 'gnome-doc-utils' 'gnome-settings-daemon' 'imagemagick' 'intltool' 'librsvg' 'vala')
optdepends=("ubuntu-themes: Ubuntu's Ambiance and Radiance themes"
            "ubuntu-wallpapers: Ubuntu's default wallpapers"
            "ttf-ubuntu-font-family: Ubuntu's default font")
install=unity-greeter.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver}-${_ubuntu_rel}.tar.gz"
        '0001-Use-gnome-settings-daemon.patch'
        'spawn_indicators.patch'
        'unity-greeter-indicators-start'
        '10-unity.defaults'
        '50-unity-greeter.rules'
        'logo.png')
sha512sums=('d82309679697c56b1bc46641e867567f5961667e33c90d7071d3ebbc66041ddbde0d0af7abf7f5fd74549caaf038b3f69ba0cbcfcb6edd5418a2760ae0760e10'
            '730a6ebb6eb16a6b48ecab297c17641dfe71ac72ecfe0e136544cc125b96f272d830a9596b019df220d03f8460541a2ecefd61769ea2d6f82345bc81f8c11d8f'
            '0fa758888e10bbf54bb1e6a4273dd3efc396ce8338aa1ab2e8f794a81c554b394805cf445c6179998786cd3d40d6eb0025382d1a2f028b5887c5b10957c4f34e'
            'e43c177d0255af961bbf9198868e32a1a762bb70e117f80c3c2ce4b54d23f4955cc1c4c32b68751b021116f8e6d26133b24845c03ae459c2209e8313e28a0bc6'
            'ee5d1f17dddd99ba55bbaca8aff5e8487c9b9f7e1eef570d2adb3d2519e19ef437b160414468ef85a8c1b14af1eee23c714e2086291701edfbab2f43064e2cd7'
            '5d0f1b8221dfe02670df3ba88011dc0ce744bfd4205a9900dd8096de222358a740a384a47f13ed4e5b94e24d4a8ff5639117464c61a689dd7bd9025a0900f529'
            '4cc7e3600a8f5afc7edf574ccdb21dbbff9c7492a46b50c696fb7b38446829f2cc99ae029688e289da2100a9cac6ae0471cc8bac03f8db3799f9929a1e2f2679')

prepare() {
  cd "${srcdir}/${pkgname#*-}-${pkgver}"

  # Apply Ubuntu patches
  for i in $(grep -v '#' debian/patches/series); do
    patch -p1 -i "debian/patches/${i}"
  done

  # We don't use unity-settings-daemon as it's incompatible with GNOME 3.12
  patch -p1 -i "${srcdir}/0001-Use-gnome-settings-daemon.patch"

  # Patch from unity-gentoo for spawning the indicator services since the
  # DBus activation method is deprecated
  patch -p1 -i "${srcdir}/spawn_indicators.patch"

  # Use Arch Linux logo (retain the same 245px height as the Ubuntu logo)
  rm data/logo.png
  # Stupid segfaulting imagemagick...
  if ! convert -background none -resize 245 \
         /usr/share/archlinux/logos/archlinux-mono-white.svg \
         data/logo.png; then
    cp "${srcdir}/logo.png" data/logo.png
  fi
}

build() {
  cd "${srcdir}/${pkgname#*-}-${pkgver}"

  # Link against libm and libx11
  export CFLAGS="${CFLAGS} -lm -lX11"

  autoreconf -vfi
  intltoolize -f

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/lightdm

   make
}

package() {
  cd "${srcdir}/unity-greeter-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Install PolicyKit file for allowing the lightdm user to use NetworkManager
  # Note: PolicyKit no longer reads pkla files after version 0.107, so Ubuntu's
  # policy file won't work.
  install -dm700 "${pkgdir}/usr/share/polkit-1/rules.d/"
  install -m644 "${srcdir}/50-unity-greeter.rules" \
    "${pkgdir}/usr/share/polkit-1/rules.d/"
  #install -dm700 "${pkgdir}/var/lib/polkit-1/"
  #install -dm755 "${pkgdir}/var/lib/polkit-1/localauthority/10-vendor.d/"
  #install -m644 "${srcdir}/debian/unity-greeter.pkla" \
  #  "${pkgdir}/var/lib/polkit-1/localauthority/10-vendor.d/"

  # Install default GSettings/dconf settings for the guest account (requires
  # lightdm-ubuntu).
  install -dm755 "${pkgdir}/etc/guest-session/gsettings/"
  install -m644 "${srcdir}/10-unity.defaults" \
    "${pkgdir}/etc/guest-session/gsettings/"

  # Install LightDM configuration file to set the Unity greeter as the default
  install -dm755 "${pkgdir}/usr/share/lightdm/lightdm.conf.d/"
  install -m644 debian/50-unity-greeter.conf \
                "${pkgdir}/usr/share/lightdm/lightdm.conf.d/"

  # Install unity-gentoo's script for launcher the indicator services
  install -m755 "${srcdir}/unity-greeter-indicators-start" "${pkgdir}/usr/bin/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"
}

# vim:set ts=2 sw=2 et:
