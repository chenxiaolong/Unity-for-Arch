# Maintainer: Xiao-Long Chen
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=libdbusmenu
pkgname=('libdbusmenu' 'libdbusmenu-gtk2' 'libdbusmenu-gtk3')
pkgver=12.10.3daily13.05.15
pkgrel=101
pkgdesc="A small little library that was created by pulling out some comon code out of indicator-applet"
arch=('i686' 'x86_64')
url="https://launchpad.net/dbusmenu"
license=('LGPL')
makedepends=('json-glib' 'gtk2-ubuntu' 'gtk3-ubuntu' 'intltool' 'gnome-doc-utils' 'gtk-doc' 'gobject-introspection' 'vala' 'python2' 'valgrind')
checkdepends=('dbus-test-runner' 'xorg-server-xvfb')
groups=('unity')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libdbusmenu_${pkgver}.orig.tar.gz"
        '0001_autoconf.patch')
sha512sums=('165827aea530f4a50ce6e918f5e41e072d7d4b456d4e8f69e12e25206ec51da862cb844d95599f0c2581ddb5032657dc6aed51de71463f8da402a6bc3ba0666f'
            '233865822a6280dbd536db131511648bdf40af042ae8afa1ed813cd2538c4e9f5f1ada8868de27bf60f1bed8830ace9916c097ba3d8d698744a2fac95a6dbabf')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  patch -p1 -i "${srcdir}/0001_autoconf.patch"
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  gtkdocize
  autoreconf -vfi
  intltoolize -f

  # Python2 fix
  sed -i "s|^\(#!.*python\)$|\12|" tools/dbusmenu-bench

  #" # Stupid vim highlighting

  # libdbusmenu-gtk2 won't install if libdbusmenu-glib is missing
  sed -i 's|$(DBUSMENUGTK_CFLAGS)|$(DBUSMENUGTK_CFLAGS) -L../libdbusmenu-glib/.libs|' libdbusmenu-gtk/Makefile.{am,in}

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-scrollkeeper --with-gtk=2 --disable-static
  export MAKEFLAGS="-j1"
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --libexecdir=/usr/lib/${pkgbase} --disable-scrollkeeper --with-gtk=3 --disable-static
  make
  popd
}

check() {
  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk3"

  make check
}

# I've modified thn81's PKGBUILD so that the directory structure of the packages
#   are identical to Ubuntu's. It should have better compatibility will other
#   packages that depend on libdbusmenu.

package_libdbusmenu() {
  depends=('json-glib')

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk3"

  export MAKEFLAGS="-j1"
  make -C libdbusmenu-glib DESTDIR="${pkgdir}/" install
  make -C tools DESTDIR="${pkgdir}/" install
  make -C tests DESTDIR="${pkgdir}/" install
  make -C docs/libdbusmenu-glib DESTDIR="${pkgdir}/" install
  make -C po DESTDIR="${pkgdir}/" install
}

package_libdbusmenu-gtk2() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('gtk2-ubuntu' "libdbusmenu=${pkgver}")

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk2"

  export MAKEFLAGS="-j1"
  make -C libdbusmenu-gtk DESTDIR="${pkgdir}/" install
  make -C docs/libdbusmenu-gtk DESTDIR="${pkgdir}/" install
}

package_libdbusmenu-gtk3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=('gtk3-ubuntu' "libdbusmenu=${pkgver}")

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk3"

  export MAKEFLAGS="-j1"
  make -C libdbusmenu-gtk DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
