# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=dee, repo=utopic
# vercheck-launchpad: name=dee

pkgname=dee-ubuntu
_actual_ver=1.2.7
_extra_ver=+15.04.20150304
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=2
epoch=1
pkgdesc="Model to synchronize multiple instances over DBus"
arch=(i686 x86_64)
url="https://launchpad.net/dee"
license=(LGPL)
groups=(unity)
depends=(dbus-glib glib2 icu python python-gobject python2 python2-gobject)
makedepends=(gnome-common gobject-introspection gtk-doc vala)
provides=("dee=${pkgver}")
conflicts=(dee)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/dee_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('c39980c413934f25c1a754546905cdbb84e0e322aa293d2f9c6b1f2a402bddfa895d576e97e09478c1162323f14c2f639e11d58f97cbf4c0c1220a7f88d6e183')

build() {
  cd "dee-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  [[ -d build-python3 ]] || mkdir build-python3
  pushd build-python3
  PYTHON=python3 ../configure --prefix=/usr --enable-gtk-doc
  make
  popd

  [[ -d build-python2 ]] || mkdir build-python2
  pushd build-python2
  PYTHON=python2 ../configure --prefix=/usr
  popd
}

package() {
  cd "dee-${_actual_ver}${_extra_ver}"

  pushd build-python3/
  make DESTDIR="${pkgdir}/" install
  popd

  pushd build-python2/bindings/python/
  make DESTDIR="${pkgdir}/" install
  popd
}
