# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

[ -z "${NO_GCC_UBUNTU}" ] && NO_GCC_UBUNTU=false

pkgname=indicator-datetime
_actual_ver=12.10.3
_extra_ver=+13.10.20130703.1
_translations=20130418
pkgver=${_actual_ver}daily13.07.03.1
pkgrel=100
pkgdesc="A very, very simple clock"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-datetime"
license=('GPL')
depends=('ido' 'evolution-data-server' 'libtimezonemap' 'geoclue')
if [ "x${NO_GCC_UBUNTU}" = "xfalse" ]; then
  depends+=('gnome-control-center-ubuntu')
else
  depends+=('gnome-control-center')
fi
makedepends=('intltool')
checkdepends=('gtest-ubuntu' 'xorg-gtest' 'xorg-util-macros')
groups=('unity')
options=('!libtool')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-indicator-datetime.tar.gz"
        '0001_Port_to_systemd_timedated.patch'
        '0003_Link_pthread.patch')
sha512sums=('c3e85a059e9d2866a1aee0aacf7cf34d183f813938bc94c2de6c597c873313a79cad0f2badc8dd08a064c22e2118ba39790247a8c171af4c175954872bb1a026'
            '5cb8d7ab7cfb1e57d9b0d3e54e09caa1cbfdf44191f32e19251983e061bf5c27812dbcb743cad17cb4ff13e6f6f22167c6d3bd0f2faa8e5a84a7bf145f90223c'
            '57c0b1846c946bcc2fffd45b933c83fa7ef14e1e19e4431238be4a406bc6565aba5c80dabaed5fc8b46d83377c71d5d2c55331599813827fa70290a9f02dd51b'
            '5c10fda83393b33b45c1fd0701e464ac1e9cc3180c3d1ebb47f5e666e57065b64f34047c913f90db5d047ffef094fe0b4ef625fe04ebeefc74b341a8f6612cf2')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Port to systemd's timedated
  #patch -p1 -i "${srcdir}/0001_Port_to_systemd_timedated.patch"

  # Tests need to link against pthread
  #patch -p1 -i "${srcdir}/0003_Link_pthread.patch"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  if [ "x${NO_GCC_UBUNTU}" = "xtrue" ]; then
    msg "NOT building GNOME Control Center panel"
  fi

  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static --disable-schemas-compile
  make -j1
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/"

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
