# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-screensaver-ubuntu
pkgver=3.6.1
_ubuntu_rel=0ubuntu5
_translations=20130418
pkgrel=10
pkgdesc="Screensaver designed to integrate well with the GNOME desktop."
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/GnomeScreensaver"
backup=('etc/pam.d/gnome-screensaver')
depends=('dbus-glib' 'libgnomekbd' 'gnome-desktop' 'gsettings-desktop-schemas' 'libsystemd')
makedepends=('pkgconfig' 'intltool' 'libxss' 'gnome-common')
provides=("gnome-screensaver=${pkgver}")
conflicts=('gnome-screensaver')
groups=('gnome')
options=('!emptydirs')
install=gnome-screensaver.install
source=("http://ftp.gnome.org/pub/GNOME/sources/gnome-screensaver/${pkgver%.*}/gnome-screensaver-${pkgver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/gnome-screensaver_${pkgver}-${_ubuntu_rel}.debian.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-gnome-screensaver.tar.gz"
        'gnome-screensaver.pam')
sha512sums=('daa140c9608859c9291966931deedad50c94dd861916601022cd508f367cdac5db68ea1abb750bbef575d19b8690617900fee9859b880b1466267fd1cbee150f'
            '71e8d0da372f4fcee59bb023421f3bad2f63bfcb77772765f2ec741b48aa507bc4a82ae4b6fc535999774fbf8b4146edbc28e9a87558645dba91f06990ee9b50'
            'a00f83b89b92feaaaa43a01807419ce8b6da95deb36af402ccf9876b49bf422183c2516a4e9b14d8d5bc0ab24f82693ac462fe23d281d6408541fac49365daa7'
            'aed0933feed70a9b16da80d6e4716641cd45d2fd3fadbace3dfeb02b1a07124ee659f470277f8ce93492c6f7cf3cc9996bc60598724eea1955975604ff8f324d')

prepare() {
  cd "${srcdir}/gnome-screensaver-${pkgver}"

  # Apply Ubuntu's patches
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/g' configure.ac

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname%-*}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/gnome-screensaver-${pkgver}"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/gnome-screensaver \
    --localstatedir=/var \
    --with-mit-ext \
    --with-systemd \
    --enable-locking # From debian/rules

  make
}

package() {
  cd "${srcdir}/gnome-screensaver-${pkgver}"

  make DESTDIR="${pkgdir}" install
  #install -Dm644 ../gnome-screensaver.pam "${pkgdir}/etc/pam.d/gnome-screensaver"
}

# vim:set ts=2 sw=2 et:
