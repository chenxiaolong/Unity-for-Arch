# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=credentials-preferences-ubuntu
_translations=20130420
pkgver=0.1.5
pkgrel=4
pkgdesc="Control panel for configuring online account credentials and settings"
arch=('i686' 'x86_64')
url="https://launchpad.net/gnome-control-center-signon"
license=('GPL' 'LGPL')
provides=('credentials-preferences')
conflicts=('credentials-preferences')
depends=('libaccounts-glib' 'libsignon-glib' 'gtk3' 'gnome-control-center-ubuntu')
makedepends=('gobject-introspection' 'vala' 'intltool' 'itstool')
options=('!libtool')
install=${pkgname}.install
source=("https://launchpad.net/gnome-control-center-signon/13.04/${pkgver}/+download/credentials-control-center-${pkgver}.tar.xz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-credentials-preferences.tar.gz")
sha512sums=('b85e3ceb07f6e7736665e1d0ac52b0e205f3e059b4457460f1fe5fb684e50e0d1b8d3f2461aec5feae30fd34ae7aafa42b4552be44fd615d9dece490926b73bb'
            '6684ab6452f793f474b8b1641182ba6254119cb82306cb4a7c296c6e080b4f451c0362fa0c90599c74ded5246e0f649fd3cc8839b976102a8ceba4fe08e657bf')

prepare() {
  cd "${srcdir}/credentials-control-center-${pkgver}"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/credentials-control-center-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/credentials-control-center-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/credentials-preferences \
    --disable-static

  make
}

package() {
  cd "${srcdir}/credentials-control-center-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
