# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Adam Hani Schakaki (krzd) <krzd@krzd.net>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname%-*}, repo=zesty
# vercheck-archlinux: name=${pkgname%-*}, repo=extra, arch=x86_64
# vercheck-gnome: name=${pkgname%-*}, majorver=3.22

pkgname=gsettings-desktop-schemas-ubuntu
_ubuntu_ver=3.22.0
_ubuntu_rel=1ubuntu1
pkgver=3.22.0+1+g6f52ab5
pkgrel=1
pkgdesc="Shared GSettings schemas for the desktop"
arch=(any)
url="https://www.gnome.org/"
license=(GPL)
depends=(glib2)
makedepends=(intltool gobject-introspection git gnome-common)
provides=("${pkgname%-*}=${pkgver}")
conflicts=("${pkgname%-*}")
_commit=6f52ab56bcf02c4a7315e433e30f45f106f31c3f
source=("git://git.gnome.org/gsettings-desktop-schemas#commit=${_commit}"
        "https://launchpad.net/ubuntu/+archive/primary/+files/gsettings-desktop-schemas_${_ubuntu_ver:-${pkgver}}-${_ubuntu_rel}.debian.tar.xz")
sha512sums=('SKIP'
            'e71dc5c3b01ecb8156aebe260d450c0f35aa6816a6b3acc43298a5e2e0f9134f8bdef6b19992d31f80d7d5333eec43d125b5c95ca1781dfd13d099ab0745bee4')

pkgver() {
    cd "${pkgname%-*}"
    git describe --tags | sed 's/-/+/g'
}

prepare() {
    cd "${pkgname%-*}"

    for i in $(grep -v '#' ../debian/patches/series); do
        msg "Applying ${i} ..."
        patch -p1 -i "../debian/patches/${i}"
    done

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${pkgname%-*}"

    ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
        --disable-schemas-compile
    make
}

package() {
    cd "${pkgname%-*}"

    make DESTDIR="${pkgdir}" install
}
