# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Adam Hani Schakaki (krzd) <krzd@krzd.net>

pkgname=gsettings-desktop-schemas-ubuntu
_ubuntu_rel=1ubuntu1
_ubuntu_ver=3.8.0
pkgver=3.8.2
pkgrel=103
pkgdesc="Shared GSettings schemas for the desktop"
arch=('any')
url="http://live.gnome.org/"
license=('GPL')
depends=('glib2')
makedepends=('intltool' 'gobject-introspection')
provides=("${pkgname%-*}=${pkgver}")
conflicts=("${pkgname%-*}")
install=${pkgname%-*}.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/gsettings-desktop-schemas_${_ubuntu_ver}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('a5ee37f5e0c6c80fee50b9ac79f417f61a88c05e996493a081ad1c5c3ef8ccd2f71be93c93ecf01aa0a3de612245ea2fe58de5281ba1417b1d83ba098568232c'
            'c89f5e3fd3850cfa5e005c23f57302331728af7b35f0f0ea9e827f8245ad14a74731a7c05238af46c61a4759a70a1697d6f8591b3cf2f9ad131bc6b42aad5899')

prepare() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  # Apply Ubuntu's patches

  # Disable patches
    sed -i '/01_no_metacity_theme.patch/d' "${srcdir}/debian/patches/series"

  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying ${i} ..."
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
