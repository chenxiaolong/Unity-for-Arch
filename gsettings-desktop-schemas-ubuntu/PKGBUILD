# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Adam Hani Schakaki (krzd) <krzd@krzd.net>

pkgname=gsettings-desktop-schemas-ubuntu
_ppa_rel=0ubuntu1~raring1
_ppa_ver=3.8.0
pkgver=3.8.2
pkgrel=101
pkgdesc="Shared GSettings schemas for the desktop"
arch=('any')
url="http://live.gnome.org/"
license=('GPL')
depends=('glib2')
makedepends=('intltool' 'gobject-introspection')
provides=("${pkgname%-*}=${pkgver}")
conflicts=("${pkgname%-*}")
install=${pkgname%-*}.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz"
        "http://ppa.launchpad.net/gnome3-team/gnome3/ubuntu/pool/main/g/gsettings-desktop-schemas/gsettings-desktop-schemas_${_ppa_ver}-${_ppa_rel}.debian.tar.gz")
sha512sums=('a5ee37f5e0c6c80fee50b9ac79f417f61a88c05e996493a081ad1c5c3ef8ccd2f71be93c93ecf01aa0a3de612245ea2fe58de5281ba1417b1d83ba098568232c'
            '2dee1ed0addbe1beb5e87f2713005433eaefa7f7819bbb7bd3327997c0bd6c179f7d9b4373d732c7c311e736d5b0fcf63eefe5411c46ce638a807e70ca31a4d3')

prepare() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  # Apply Ubuntu's patches
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    msg "Applying ${i} ..."
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done
}

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
