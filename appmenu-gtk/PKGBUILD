# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgbase=appmenu-gtk
pkgname=('appmenu-gtk2' 'appmenu-gtk3')
_actual_ver=12.10.3
_extra_ver=daily13.04.03
pkgver=${_actual_ver}${_extra_ver}
pkgrel=101
arch=('i686' 'x86_64')
url="https://launchpad.net/appmenu-gtk"
license=('LGPL')
makedepends=('gnome-common' 'gtk2-ubuntu' 'gtk3-ubuntu' 'libdbusmenu-gtk2' 'libdbusmenu-gtk3')
groups=('unity')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/appmenu-gtk_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_autoconf.patch')
sha512sums=('26804c39de196a508df785d9c8ec964f7676cad92ee24da97eb4bebd9709bc2076a7d1255595de50d20e7e1ab47bfad96f78e60d119421bb260ff65dbc46c0a2'
            '82011361d720d8712ba3ece7dd2340694eab226f21263f861d8629ed39c343c873f1fb49f5c9cb302a6a5486fca098f36013b480c19d3bbe8745ccca84baa657')

prepare() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_autoconf.patch"
}

build() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  export CFLAGS="${CFLAGS} -Wno-error"

  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk2 --disable-static
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk3 --disable-static
  make
  popd
}

package_appmenu-gtk2() {
  pkgdesc="Adds appmenu support for GTK+ 2 apps"
  depends=('libdbusmenu-gtk2')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk2/"

  make DESTDIR="${pkgdir}/" install
  install -Dm755 "${pkgdir}/etc/X11/Xsession.d/80appmenu" "${pkgdir}/etc/X11/xinit/xinitrc.d/80appmenu"
  rm -rvf "${pkgdir}/etc/X11/Xsession.d/"
}

package_appmenu-gtk3() {
  pkgdesc="Adds appmenu support for GTK+ 3 apps"
  depends=('libdbusmenu-gtk3')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk3/"

  make DESTDIR="${pkgdir}/" install
  install -Dm755 "${pkgdir}/etc/X11/Xsession.d/80appmenu-gtk3" "${pkgdir}/etc/X11/xinit/xinitrc.d/80appmenu-gtk3"
  rm -rvf "${pkgdir}/etc/X11/Xsession.d/"
}

# vim:set ts=2 sw=2 et:
