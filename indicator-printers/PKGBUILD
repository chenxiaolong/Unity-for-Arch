# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-printers
_ubuntu_rel=0ubuntu1
_actual_ver=0.1.7
_extra_ver=+15.04.20150220
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=2
epoch=1
pkgdesc="Indicator showing active print jobs"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-printers"
license=(GPL)
groups=(unity)
depends=(cups libdbusmenu-glib libdbusmenu-gtk3 libindicator3)
makedepends=(intltool python2)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-printers_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/indicator-printers_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz")
sha512sums=('c4aff3d515d64c78085175c5ff807b32c3fcc501dc879247a34545cdfc25d45d445fe7c56b661ce03953020246960b3c55110a44d015807abad9cc90b9c2198d'
            '2ed2e49bc6a04ea02a7feb229690938aec48a0b4389534a64b95fdbef61bfd0ea3ab0645fc4d8145c5a3c4feca81d83f2beb39a2ab374f088c59efd14108cd89')

prepare() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    # Apply Ubuntu's patches
    patch -p1 -i ../indicator-printers_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff

    sed -i '/SERVICE_LIBS/ i SERVICE_LIBS+=" "' configure.ac
}

build() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    ./autogen.sh --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
    make
}

package() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    make DESTDIR="${pkgdir}/" install

    install -dm755 "${pkgdir}"/usr/share/icons/
    cp -r debian/local/* "${pkgdir}"/usr/share/icons/

    rm -r "${pkgdir}/usr/share/upstart/"
}
