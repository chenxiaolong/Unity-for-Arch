# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=indicator-printers
_ubuntu_rel=0ubuntu1
_actual_ver=0.1.7
_extra_ver=daily13.03.01
_translations=20130418
pkgver=${_actual_ver}${_extra_ver}
pkgrel=106
pkgdesc="Indicator showing active print jobs"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-printers"
license=('GPL')
groups=('unity')
depends=('cups' 'libdbusmenu-glib' 'libdbusmenu-gtk3' 'libindicator3')
makedepends=('intltool' 'python2')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-printers_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/indicator-printers_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-indicator-printers.tar.gz")
sha512sums=('443686eb599230bd81bae60d4feeeea0dc39526a083bde7f4f4a90f53bcd7ce46d38cd7f1b531397b8f036ce4c13d189105676c4d81d268ed5abb35024a626d1'
            '34d660d3f5dfa19115f77f6a290609c21cb3dd65660528b68a0a4d12df3f36edea83de5692ae3139963743d6e60d72f77cff03c7fc0efa0fe09105520415706a'
            'edbf2e0db90b81b805f966ddc3e1d1fab76f50651eb5135f3c559daca30b6134e72b2fbd01ccdd357a3252d096345a42e17c62a986fe3c3477034d1cf7624759')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Apply Ubuntu's patches
  patch -p1 -i "${srcdir}/indicator-printers_${_actual_ver}${_extra_ver}-${_ubuntu_rel}.diff"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
