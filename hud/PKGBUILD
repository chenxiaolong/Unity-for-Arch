# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=hud
_actual_ver=13.10.1
_extra_ver=+13.10.20130703
pkgver=${_actual_ver}daily13.07.03
pkgrel=100
pkgdesc="Backend for the Unity HUD"
arch=('i686' 'x86_64')
url="https://launchpad.net/hud"
license=('GPL')
groups=('unity')
depends=('bamf' 'dee' 'libdbusmenu-gtk3' 'sqlite')
# Speech recognition
depends+=('pocketsphinx' 'sphinx-voxforge-en' 'sphinxbase' 'pulseaudio')
makedepends=('cmake' 'gnome-common' 'gnome-doc-utils' 'gobject-introspection' 'gtk-doc' 'intltool' 'python2-distribute' 'vala')
checkdepends=('dbus-test-runner' 'xorg-server-xvfb')
optdepends=('indicator-appmenu: HUD support')
install=hud.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/hud_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_Python_2.patch')
sha512sums=('012cf4be2891da1dc2ebc212fb6ed48b5bae3d93c3b4603f878cee8621236dba9d6559f7f62b287502c53f1217050abb4879f2ea1d4be2cc3436c5b1ebfd7337'
            '1d560db046c56ecff0d6566c98684962f179326009fb2bfdbaab3fc1df265afa5cb2da202fb13b4889ade0244eaf39f3aea4d7c9e434c2bd1334f75bc969cc90')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_Python_2.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  TESTS=ON
  if [ ! -x /usr/bin/dbus-test-runner ]; then
    TESTS=OFF
  fi

  mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib \
    -DENABLE_DOCUMENTATION=ON \
    -DENABLE_TESTS=${TESTS} \
    -DENABLE_VOICE_TESTS=${TESTS} `# Off on Ubuntu` \
    -DENABLE_SCALABILITY_TESTS=${TESTS} \
    -DFULL_WARNINGS=ON

  make -j1
}

check() {
  # Will fix tests later
  return 0

  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/build"

  # Tests require gtester2xunit, dbus-test-runner, and bustle

  local EXTRAPATH="${srcdir}/bin"
  local EXTRALIBS=""

  rm -rf "${srcdir}/bin/"
  mkdir "${srcdir}/bin/"
  pushd "${srcdir}/bin/"

  # gtester2xunit
  wget 'https://bazaar.launchpad.net/~autopilot/gtester2xunit/trunk/download/head:/gtester2xunit-20121028102712-vb10md6zm7ydek6w-2/gtester2xunit'
  wget 'https://bazaar.launchpad.net/~autopilot/gtester2xunit/trunk/download/head:/gtester.xsl-20121028102712-vb10md6zm7ydek6w-3/gtester.xsl'
  chmod +x gtester2xunit
  sed -i -e 's|^\(#!.*python$\)|\12|g' \
         -e "s|/usr/share/gtester2xunit/gtester.xsl|$(pwd)/gtester.xsl|g" \
      gtester2xunit

  # bustle
  case ${CARCH} in
  i*86) BUSTLE_ARCH=i486     ;;
  *)    BUSTLE_ARCH=${CARCH} ;;
  esac

  wget "http://willthompson.co.uk/bustle/releases/0.4.1/bustle-0.4.1-${BUSTLE_ARCH}.tar.bz2"
  tar jxvf bustle-0.4.1-${BUSTLE_ARCH}.tar.bz2
  EXTRAPATH+=":$(pwd)/bustle-0.4.1-${BUSTLE_ARCH}/bin"
  EXTRALIBS+=":$(pwd)/bustle-0.4.1-${BUSTLE_ARCH}/lib"

  popd

  LD_LIBRARY_PATH="${LD_LIBRARY_PATH}:${EXTRALIBS}" \
    PATH="${PATH}:${EXTRAPATH}" \
    ctest
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/build"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
