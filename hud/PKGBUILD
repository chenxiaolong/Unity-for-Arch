# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=hud
_actual_ver=13.04.0
_extra_ver=daily13.04.03
pkgver=${_actual_ver}${_extra_ver}
pkgrel=103
pkgdesc="Backend for the Unity HUD"
arch=('i686' 'x86_64')
url="https://launchpad.net/hud"
license=('GPL')
groups=('unity')
depends=('bamf' 'libdbusmenu-gtk3' 'sqlite')
makedepends=('gnome-doc-utils' 'gtk-doc' 'intltool' 'vala')
checkdepends=('dbus-test-runner' 'xorg-server-xvfb' 'libxslt' 'wget')
optdepends=('indicator-appmenu: HUD support')
conflicts=('indicator-appmenu<13')
install=hud.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/hud_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_autotools.patch'
        '0003_Include_string.h.patch')
sha512sums=('95cb5a85951f0badcd671abd58e768099dbac66735c35015b23f5149c3804eec19db633c3435e10ce927ca6dcad38f884e3cb2214709528277de3712436b3c55'
            '2fb93095182e736fcf1a45b6dbe5707688e7724688969848a9d0fbf24b4b6b761061963ec94b667ae3e23048299af2e09e50691d6dd6e01889d47e7fb4afa375'
            '047124d0a324212d67aa2f33dcd83b9112323e17dc784198ff4d9d10c0fd8613466a78739fa3c78c59e183a6b03db13fadcd01496a1d1b93e48427756cf2c3cd')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  patch -p1 -i "${srcdir}/0001_autotools.patch"
  patch -p1 -i "${srcdir}/0003_Include_string.h.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  gtkdocize
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/hud
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Tests require gtester2xunit, dbus-test-runner, and bustle

  local EXTRAPATH="${srcdir}/bin"
  local EXTRALIBS=""

  rm -rf "${srcdir}/bin/"
  mkdir "${srcdir}/bin/"
  pushd "${srcdir}/bin/"

  # gtester2xunit
  wget 'https://bazaar.launchpad.net/~autopilot/gtester2xunit/trunk/download/head:/gtester2xunit-20121028102712-vb10md6zm7ydek6w-2/gtester2xunit'
  wget 'https://bazaar.launchpad.net/~autopilot/gtester2xunit/trunk/download/head:/gtester.xsl-20121028102712-vb10md6zm7ydek6w-3/gtester.xsl'
  chmod +x gtester2xunit
  sed -i -e 's|^\(#!.*python$\)|\12|g' \
         -e "s|/usr/share/gtester2xunit/gtester.xsl|$(pwd)/gtester.xsl|g" \
      gtester2xunit

  # bustle
  case ${CARCH} in
  i*86) BUSTLE_ARCH=i486     ;;
  *)    BUSTLE_ARCH=${CARCH} ;;
  esac

  wget "http://willthompson.co.uk/bustle/releases/0.4.1/bustle-0.4.1-${BUSTLE_ARCH}.tar.bz2"
  tar jxvf bustle-0.4.1-${BUSTLE_ARCH}.tar.bz2
  EXTRAPATH+=":$(pwd)/bustle-0.4.1-${BUSTLE_ARCH}/bin"
  EXTRALIBS+=":$(pwd)/bustle-0.4.1-${BUSTLE_ARCH}/lib"

  popd

  LD_LIBRARY_PATH="${LD_LIBRARY_PATH}:${EXTRALIBS}" \
    PATH="${PATH}:${EXTRAPATH}" \
    make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
