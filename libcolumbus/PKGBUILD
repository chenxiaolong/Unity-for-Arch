# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=libcolumbus
_actual_ver=0.4.0
_extra_ver=daily13.04.16~13.04
pkgver=${_actual_ver}.${_extra_ver/\~/.}
pkgrel=102
pkgdesc="A small, fast, error tolerant matcher"
arch=('i686' 'x86_64')
url="https://launchpad.net/libcolumbus"
license=('LGPL')
groups=('unity')
depends=('icu')
makedepends=('boost' 'cmake' 'sparsehash')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libcolumbus_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('0b241e4739baf5b652e112264df4bd3b013413728d48f9be949457cd4d253b3ae63ea173d94dac165cc08ba3df33604eccaf3385a263d814f2aee35c9e1ce38f')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/build"
  export COLUMBUS_DATADIR=$(pwd)/../share
  ctest
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/build"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
