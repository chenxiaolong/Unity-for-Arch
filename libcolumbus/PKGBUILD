# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety
# vercheck-launchpad: name=${pkgname}

pkgname=libcolumbus
_actual_ver=1.1.0
_extra_ver=+15.10.20150806
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=4
epoch=1
pkgdesc="A small, fast, error tolerant matcher"
arch=(i686 x86_64)
url="https://launchpad.net/libcolumbus"
license=(LGPL)
groups=(unity)
depends=(icu)
makedepends=(boost cmake sparsehash)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/libcolumbus_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('72a67115c813ceb575eb237c28eb096232e319524718f8fa4d28dab2fd42bf3eb637366ac829d9fb0999d119413b5259d6802de3beb5dff52983ce80ffe5fe41')

build() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"
    mkdir -p build
    cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_BUILD_TYPE=''
    make
}

package() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}/build"
    make DESTDIR="${pkgdir}/" install
}
