# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=gmock-ubuntu
pkgver=1.6.0
_ubuntu_rel=0ubuntu3
pkgrel=4
pkgdesc="Google's framework for writing and using C++ mock classes"
arch=('i686' 'x86_64')
url="http://code.google.com/p/googlemock/"
license=('BSD')
groups=('unity-tests')
depends=('gtest-ubuntu' 'python2')
provides=("gmock=${pkgver}")
conflicts=('gmock')
options=('!libtool' '!emptydirs')
source=("https://googlemock.googlecode.com/files/gmock-${pkgver}.zip"
        "https://launchpad.net/ubuntu/+archive/primary/+files/google-mock_${pkgver}-${_ubuntu_rel}.debian.tar.gz")
sha512sums=('7adff00bb6ff81a6ffb704be71d0747636d69e24b4a8b709e1fb8c177a9a47f6dea8d14fde73034ebfb1529c291c7212a944ad92a34f88abdfe23c2d0b11b6f7'
            'c6f0aecf4b0e18bfca06736c50b11dedc09942ed452edb052220d776f55ae2443d8b93c4e0ae1dbacd1ed3e0f73663f4d280918e74b6b46ae422bee2a003f096')

build() {
  cd "${srcdir}/gmock-${pkgver}"

  find -type f -exec chmod u+w {} \+
  find -type f -name '*.py' -exec sed -i 's|^\(#!.*python$\)|\12|g' {} \+

  # Apply Ubuntu's patches
  for i in $(grep -v '#' "${srcdir}/debian/patches/series"); do
    patch -p1 -i "${srcdir}/debian/patches/${i}"
  done

  ./configure --enable-static=no --prefix=/usr --enable-external-gtest
  make
}

check() {
  cd "${srcdir}/gmock-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/gmock-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}/usr/bin/"
  install -m755 scripts/generator/gmock_gen.py "${pkgdir}/usr/bin/"

  install -dm755 "${pkgdir}/usr/share/gmock/cpp/"
  install -m644 scripts/generator/cpp/* "${pkgdir}/usr/share/gmock/cpp/"

  install -dm755 "${pkgdir}/usr/share/man/man1/"
  install -m644 "${srcdir}/debian/gmock_gen.1" "${pkgdir}/usr/share/man/man1/"

  rm -rv "${pkgdir}/usr/include/gtest/"
  rm -v "${pkgdir}"/usr/lib/libgtest*
  rm -v "${pkgdir}/usr/share/aclocal/gtest.m4"

  for i in "${pkgdir}"/usr/bin/*.py; do mv ${i} ${i%.py}; done

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
