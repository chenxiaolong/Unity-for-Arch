# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgbase}, repo=utopic
# vercheck-launchpad: name=${pkgbase}

pkgname=(overlay-scrollbar liboverlay-scrollbar liboverlay-scrollbar3)
pkgbase=overlay-scrollbar
_actual_ver=0.2.16
_extra_ver=+r359+15.04.20150319
pkgver=${_actual_ver}${_extra_ver//\+/.}
pkgrel=1
epoch=2
pkgdesc="Overlaid Scrollbars that take up no active screen real-estate"
arch=(i686 x86_64)
url="https://launchpad.net/ayatana-scrollbar"
license=(LGPL)
groups=(unity)
makedepends=(gtk2-ubuntu gtk3)
options=(!emptydirs)
extrafiles=(overlay-scrollbar.install)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/overlay-scrollbar_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('18b3527af63db3e1bac848db36931c7bc6d6f5b3d0b89df0eb84e3f06be751157d819d606d0466596036ae282079dc767ed981a9e5e532db277d9ed63e8b9d2f')

build() {
  cd "${pkgbase}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=2 --disable-static
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=3 --disable-static
  make
  popd
}

package_overlay-scrollbar() {
  pkgdesc="Scrollbar overlayed widget"
  depends=(dconf liboverlay-scrollbar liboverlay-scrollbar3)
  install=overlay-scrollbar.install

  cd "${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk3/"
  make DESTDIR="${pkgdir}/" install

  rm -rvf "${pkgdir}/usr/lib/"

  # Put X11 startup script in correct directory
  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  mv "${pkgdir}/etc/X11/Xsession.d/81overlay-scrollbar" \
    "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  chmod 755 "${pkgdir}/etc/X11/xinit/xinitrc.d/81overlay-scrollbar"
}

package_liboverlay-scrollbar3() {
  pkgdesc="Scrollbar overlayed widget - GTK 3 library"
  depends=(cairo gtk3)

  cd "${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk3/"
  make DESTDIR="${pkgdir}/" install

  rm -rvf "${pkgdir}/etc/"
  rm -rvf "${pkgdir}/usr/share/glib-2.0/"
}

package_liboverlay-scrollbar() {
  pkgdesc="Scrollbar overlayed widget - GTK 2 library"
  depends=(gtk2-ubuntu)

  cd "${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk2/"
  make DESTDIR="${pkgdir}/" install

  rm -rvf "${pkgdir}/etc/"
  rm -rvf "${pkgdir}/usr/share/glib-2.0/"
}
