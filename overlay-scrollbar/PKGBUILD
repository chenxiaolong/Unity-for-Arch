# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=('liboverlay-scrollbar' 'liboverlay-scrollbar3')
pkgbase=overlay-scrollbar
pkgver=0.2.16
pkgrel=100
pkgdesc="Ayatana Scrollbars use an overlay to ensure that scrollbars take up no active screen real-estate."
arch=('i686' 'x86_64')
url="https://launchpad.net/ayatana-scrollbar"
license=('LGPL')
groups=('unity')
makedepends=('glib2-ubuntu' 'gtk2-ubuntu' 'gtk3-ubuntu')
options=('!libtool')
source=("http://launchpad.net/ayatana-scrollbar/${pkgver%.*}/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz")
sha512sums=('c190664451ba2edf92d3e46e2353635d35ddd7dea6cda02b7e05d7effa3c2cb5cc622bae695bd88e853fd2649e1862008aa986546cf0b39fa226b23207ca721e')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  sed -i 's/3\.3\.6/3.2.3/' configure.ac
  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=2 --disable-static
  make ${MAKEFLAGS}
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=3 --disable-static
  make ${MAKEFLAGS}
  popd
}

package_liboverlay-scrollbar3() {
  pkgdesc="Scrollbar overlayed widget - GTK 3 library"
  depends=('glib2-ubuntu' 'gtk3-ubuntu')
  provides=('overlay-scrollbar')
  conflicts=('overlay-scrollbar')

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk3"
  MAKEFLAGS="-j1"
  make DESTDIR="${pkgdir}/" install

  rm -rvf "${pkgdir}/etc/"
}

package_liboverlay-scrollbar() {
  pkgdesc="Scrollbar overlayed widget - GTK 2 library"
  depends=('gtk2-ubuntu')

  cd "${srcdir}/${pkgbase}-${pkgver}/build-gtk2"
  MAKEFLAGS="-j1"
  make DESTDIR="${pkgdir}/" install

  rm -rvf "${pkgdir}/etc/"
}
