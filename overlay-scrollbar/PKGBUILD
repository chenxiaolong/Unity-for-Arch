# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=('overlay-scrollbar' 'liboverlay-scrollbar' 'liboverlay-scrollbar3')
pkgbase=overlay-scrollbar
_actual_ver=0.2.16
_extra_ver=+r359daily13.02.06
# Use 1_${pkgver} instead of an epoch because dropbox is stupid:
#   http://www.dropbox.com/.../a-1:1.0-1.pkg.tar.xz is downloaded as:
#                              a-1%3A1.0-1.pkg.tar.xz with curl, but downloading
#   http://www.dropbox.com/.../a-1%3A1.0-1.pkg.tar.xz results in 404.
# What kind of people do they hire to run their webservers?
pkgver=1_${_actual_ver}${_extra_ver/+/.}
pkgrel=100
pkgdesc="Ayatana Scrollbars use an overlay to ensure that scrollbars take up no active screen real-estate."
arch=('i686' 'x86_64')
url="https://launchpad.net/ayatana-scrollbar"
license=('LGPL')
groups=('unity')
makedepends=('gtk2-ubuntu' 'gtk3-ubuntu')
options=('!libtool' '!emptydirs')
extrafiles=('overlay-scrollbar.install')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/overlay-scrollbar_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('cde01f038b29b464aef9543cf8891275832ed82c0b5ca27c22228d35f4d68089e97e148c46ba3a1bbf341dc370566f4201e3c5dee3bdf9cc074a287230773fdd')

build() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=2 --disable-static
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --with-gtk=3 --disable-static
  make
  popd
}

check() {
  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk2/"
  make check

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk3/"
  make check
}

package_overlay-scrollbar() {
  pkgdesc="Scrollbar overlayed widget"
  depends=('liboverlay-scrollbar' 'liboverlay-scrollbar3')
  install=overlay-scrollbar.install

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk3/"
  make DESTDIR="${pkgdir}/" install

  rm -rvf "${pkgdir}/usr/lib/"

  # Put X11 startup script in correct directory
  install -dm755 "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  mv "${pkgdir}/etc/X11/Xsession.d/81overlay-scrollbar" \
    "${pkgdir}/etc/X11/xinit/xinitrc.d/"
  chmod 755 "${pkgdir}/etc/X11/xinit/xinitrc.d/81overlay-scrollbar"
}

package_liboverlay-scrollbar3() {
  pkgdesc="Scrollbar overlayed widget - GTK 3 library"
  depends=('gtk3-ubuntu')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk3/"
  make DESTDIR="${pkgdir}/" install

  rm -rvf "${pkgdir}/etc/"
  rm -rvf "${pkgdir}/usr/share/glib-2.0/"
}

package_liboverlay-scrollbar() {
  pkgdesc="Scrollbar overlayed widget - GTK 2 library"
  depends=('gtk2-ubuntu')

  cd "${srcdir}/${pkgbase}-${_actual_ver}${_extra_ver}/build-gtk2/"
  make DESTDIR="${pkgdir}/" install

  rm -rvf "${pkgdir}/etc/"
  rm -rvf "${pkgdir}/usr/share/glib-2.0/"
}

# vim:set ts=2 sw=2 et:
