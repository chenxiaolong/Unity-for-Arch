# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=dbus-test-runner
_actual_ver=12.10.2
_extra_ver=daily13.02.26
pkgver=${_actual_ver}${_extra_ver}
pkgrel=103
pkgdesc="Run tests under a new DBus session"
arch=('i686' 'x86_64')
url="https://launchpad.net/dbus-test-runner"
license=('GPL')
groups=('unity-tests')
depends=('dbus-glib' 'gvfs' 'xorg-server-xvfb')
makedepends=('gnome-common' 'intltool')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/dbus-test-runner_${_actual_ver}${_extra_ver}.orig.tar.gz"
        '0001_autotools.patch')
sha512sums=('cd880d8cf74e9a7c001e4d422f7675d0d9d6b54a1529c535a8d5393d7a44ee274b0e2b0a9b9345dfb50fe517fa3d9b0ab18642691f9c30914aa915c266b7d4e9'
            '9422487c0fc573d4d43b473568744af7b354a1608155d5daa6248612e1e2d970c1ce5bd4448a50702b2fae366131f686767066ba8ba0b97f9467bbb2034dfc6b')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  patch -p1 -i "${srcdir}/0001_autotools.patch"
  export CFLAGS="${CFLAGS} -Wno-error"
  autoreconf -vfi
  ./configure --enable-static=no --libexecdir=/usr/lib --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
