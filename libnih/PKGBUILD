# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=libnih
_ubuntu_rel=4.3ubuntu1
pkgver=1.0.3
pkgrel=1
pkgdesc="Light-weight library of standard C library functions"
arch=(i686 x86_64)
url="https://launchpad.net/libnih"
license=(GPL)
groups=(unity)
depends=(dbus expat)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}-${_ubuntu_rel}.diff.gz")
sha512sums=('fce40d2445b28c27b8838631681ca3206a4f053b2dd4fc488fc9ef98bbd3d933e3d62b82cf346be2ef1677f6457f692cf5544cd915a6bb1e5c618f98ffa101b4'
            'acf748a7906ca8d320a7cec4d01ebb98fbe8631f84e65a6dd2c927da55c2384b998686d2298d095f2b48e10d25bab5c3586a479ae83fc4e6a92fed1b9060e7ff')

prepare() {
	cd "${pkgname}-${pkgver}"
    patch -p1 -i "${srcdir}/${pkgname}_${pkgver}-${_ubuntu_rel}.diff"
}

build() {
	cd "${pkgname}-${pkgver}"

    # From debian/rules
    export CFLAGS="-Wall -fstack-protector -fPIE ${CFLAGS}"
    export LDFLAGS="-Wl,--as-needed -Wl,-z,relro -Wl,-z,now -pie ${LDFLAGS}"

    autoreconf -vfi
	./configure --prefix=/usr
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}/" install
}
