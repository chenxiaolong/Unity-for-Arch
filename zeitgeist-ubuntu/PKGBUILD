# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname%-*}, repo=zesty
# vercheck-launchpad: name=${pkgname%-*}

pkgname=zeitgeist-ubuntu
pkgver=0.9.16
_ubuntu_rel=0.2ubuntu1
pkgrel=3
pkgdesc="Service logging user activities and events"
arch=(i686 x86_64)
url="http://zeitgeist-project.com/"
license=(GPL2 LGPL2.1)
depends=(json-glib telepathy-glib gtk3 xapian-core)
makedepends=(intltool gobject-introspection vala raptor python2-rdflib gnome-common)
provides=(zeitgeist-datahub "zeitgeist=${pkgver}")
conflicts=(zeitgeist-datahub zeitgeist)
replaces=(zeitgeist-datahub)
source=("https://launchpad.net/zeitgeist/${pkgver%.*}/${pkgver}/+download/zeitgeist-${pkgver}.tar.xz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/zeitgeist_${pkgver}-${_ubuntu_rel}.debian.tar.xz")
sha512sums=('eb28215c937b71c8cec12f55236db4281ad529b2854ecb3c093477d45cd8348c6ea764e2cf00a99d41645b27b685a5325f0631b318409735b578a5871087b0f1'
            '9e8974fac67473313d743373738ce4afd55b20c293197283808f1f3a07f49468638bfbe779432662afd44ff684786cc3470036df064d0326b942608f1f2f641a')

prepare() {
    cd "zeitgeist-${pkgver}"

    sed -i 's/python -/$PYTHON -/' configure configure.ac
    sed -i '1s/python$/python2/' data/ontology2code

    for i in $(grep -v '#' ../debian/patches/series); do
        msg "Applying ${i}"
        patch -p1 -i "../debian/patches/${i}"
    done
}

build() {
    cd "zeitgeist-${pkgver}"
    export PYTHON=/usr/bin/python2
    autoreconf -vfi
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/zeitgeist \
        --enable-datahub \
        --enable-fts \
        --enable-telepathy
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
    make
}

package() {
    cd "zeitgeist-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
