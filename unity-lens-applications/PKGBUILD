# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=unity-lens-applications
_actual_ver=6.10.0
_extra_ver=daily13.04.18~13.04
_translations=20130418
pkgver=${_actual_ver}.${_extra_ver/\~/.}
pkgrel=100
pkgdesc="A daemon that exposes you applications with their usage statistics and status to Unity"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-lens-applications"
license=('GPL')
depends=('dconf' 'gnome-menus' 'libcolumbus' 'libunity' 'libzeitgeist' 'xapian-core')
makedepends=('vala' 'intltool')
groups=('unity')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-lens-applications_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-unity-lens-applications.tar.gz"
        '10-no-db51.patch')
sha512sums=('8f867f6e17c7ad0babfb9f5a595bccb5054d329f48168309ec01e67cadaaa604f409c729e3388351a811cecd89e246d9443663f586caa9fa3e4f13e5bed5c7c3'
            '373cd206980e64d388335a3396091a2997a27d27334352073d6cfb927b4e9f04d33f70420859cd522b562c75377938867500e73c47a75f1c8dce9f86d5a1014d'
            '51126591417c1edf29fe7bfcd01ea7f61456505cc6a2a37ca8e67d2b53d8622bdc631765741f2eea36413ab036cfc0e6e0e1dfd7d53e0bf10e50f81bbc9883d9')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  # Remove check for db version 5.1
  patch -p1 -i "${srcdir}/10-no-db51.patch"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  aclocal --install --force
  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libexecdir=/usr/lib/${pkgname} --disable-schemas-compile
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
