# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=unity-lens-applications
pkgver=6.8.0
pkgrel=100
pkgdesc="A daemon that exposes you applications with their usage statistics and status to Unity"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-lens-applications"
license=('GPL')
depends=('dconf' 'libzeitgeist' 'libunity' 'gnome-menus2' 'xapian-core')
makedepends=('vala' 'intltool')
groups=('unity')
install=${pkgname}.install
source=("http://launchpad.net/${pkgname}/${pkgver%%.*}.0/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        '10-no-db51.patch')
sha512sums=('64cb47879e49d6efffa2fb62d38be6e1dac14e4a2ed7e2fe3eb6ef755c9a08f1e6dd332d789361eb91b7a652b9f2427d903a651a53e19297de23a9da381a1896'
            '51126591417c1edf29fe7bfcd01ea7f61456505cc6a2a37ca8e67d2b53d8622bdc631765741f2eea36413ab036cfc0e6e0e1dfd7d53e0bf10e50f81bbc9883d9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Remove check for db version 5.1
  patch -Np1 -i "${srcdir}/10-no-db51.patch"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static --libexecdir=/usr/lib/${pkgname} --disable-schemas-compile
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
