# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-applet
_actual_ver=12.10.2
_extra_ver=+13.10.20130719
_translations=20130418
pkgver=${_actual_ver}daily13.07.19
pkgrel=100
pkgdesc="A small applet to display information from various applications consistently in the panel"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-applet"
license=('GPL')
depends=('ido' 'libindicator3' 'xdg-utils' 'gnome-panel')
makedepends=('intltool')
optdepends=('indicator-application: take menus from applications and place them in the panel'
            'indicator-appmenu: host the menus from an application'
            'indicator-datetime: a very, very simple clock'
            'indicator-messages: a place on the users desktop that collects messages that need a response'
            'indicator-power: show the power status of your devices'
            'indicator-session: change your status, switch users'
            'indicator-sound: a unified sound menu')
groups=('unity')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-applet_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-indicator-applet.tar.gz"
        '0001_autoconf.patch')
sha512sums=('6c65f5eae95d829046ab5bf1c9478d990c2232254505fe0383e48b5a0dc1e1c4c6b07d47ebbf19a1b5424fe1164e7c12b139101345892ff9e3da1d7199cc08b5'
            '18a72bf4843e79b8a23e21e485a1bac4d3a417b53ee1da4188472b2d17a22697c5bf99477905a3f08958476a2438781497e49d0e70fe2482f818e5d64eb878d4'
            '231066c0b9fae5b4cf3cc0cf126a67a287855abc16d610d9a8e7d78d7d095e7c37fccfe5768666c581f583a1482b3043997096c3820dc75389e478a033f8826a')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  patch -p1 -i "${srcdir}/0001_autoconf.patch"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  aclocal --install
  autoreconf -vfi
  intltoolize -f
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
