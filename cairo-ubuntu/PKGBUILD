# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>

pkgname=cairo-ubuntu
_actual_ver=1.13.0
_extra_ver='~20140204'
pkgver=${_actual_ver}${_extra_ver/\~/.}
pkgrel=1
pkgdesc="Cairo vector graphics library"
arch=('i686' 'x86_64')
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng' 'libxrender' 'libxext' 'fontconfig' 'pixman>=0.28.0' 'glib2' 'mesa' 'libgl' 'sh' 'lzo2')
makedepends=('mesa-libgl' 'librsvg' 'gtk2' 'poppler-glib' 'libspectre' 'gtk-doc' 'valgrind')
provides=('cairo-xcb' "cairo=${_actual_ver}")
replaces=('cairo-xcb')
conflicts=('cairo')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/cairo_${_actual_ver}${_extra_ver}.orig.tar.xz")
sha512sums=('334227a1beaaf667cdb897d9981261e57df6acc81873a92724627fe31c64b6433b5fd9e6b72d10d8113d966aee76cd24b797738b341d22d98b95eebebdb7a4c1')

build() {
  cd "cairo-1.13.1"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-tee \
    --enable-gl \
    --enable-egl \
    --enable-svg \
    --enable-ps \
    --enable-pdf \
    --enable-gobject
	
  make
}

package() {
  cd "cairo-1.13.1"
  make DESTDIR="${pkgdir}" install
}
