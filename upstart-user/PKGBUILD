# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=upstart, repo=yakkety
# vercheck-launchpad: name=upstart

pkgname=upstart-user
_ubuntu_rel=0ubuntu26
pkgver=1.13.2
pkgrel=5
pkgdesc="Event-based user sessions service management daemon"
arch=(i686 x86_64)
url="http://upstart.ubuntu.com/"
license=(GPL)
groups=(unity)
depends=(dconf json-c libnih-ubuntu cgmanager)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/upstart_${pkgver}.orig.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/upstart_${pkgver}-${_ubuntu_rel}.diff.gz"
		00-upstart
		99z-upstart
		upstart-xsessions
        dbus.conf)
sha512sums=('07a3db5547996772bd6fe0ecb323214698634490eec36b7753a6d0a3ee07caa456696d0fbd9c031d61875dd4b27c9f2bbac37c7511acc54b7bb0146662e35520'
            'd6bfb694f2d755cd41bfd70ca78c6b442fd457ee1a4ef55929dbcae69eb1c13563faaa76b065238d537bf2f4e0b01c3a34b2c33788a1c9e06e3f93ea30a38c44'
            '7894335605e1960258ebf2791e1cd37ce9932ed3fbef0ce923454eed69792d7c1a013e22463c99654f4b62ca447a956abd2e01901cbc0f2dcedb370dfe13c9e6'
            '090f53ee2af576745d16b3a1372aefda9ad3c7682f7bd963718076110e54c767e6e9d009d66bc49984e9ffa7be510170dd780ef8e25ba7ccfde2362aa06ecd8b'
            '7e6d1e78975b3b5b368732a19bc9d67520055192efc54951fa8dc1567180a365ae57542272f85acaf59489172c7c7131dd6a73f58825b6f0abc0893c2cd91c12'
            '50efd5c917ec6bfaff8af57620518c31f015678efb738bb20bfe0b8dc0c10670f59d0987b858ed152c4c453f7ca284deaee8c69db74ca6fc02132c5ca23da876')

prepare() {
	cd "upstart-${pkgver}"

    # The Ubuntu diffs are incorrectly formatted. If a hunk is supposed to
    # create a new file, but the file already exists, then it appends to the
    # file instead. This breaks rebuilds without clearing src/. We'll work
    # around this by deleting any new files created by the diff.
    rm -vf dbus/org.freedesktop.DBus.xml
    rm -vf extra/com.ubuntu.Upstart.xml
    rm -vf extra/conf-session/upstart-udev-bridge.conf
    rm -vf extra/org.freedesktop.systemd1.xml
    rm -rvf debian/

	patch -p1 -i "${srcdir}/upstart_${pkgver}-${_ubuntu_rel}.diff"

    rm debian/user-conf/logrotate.conf
}

build() {
	cd "upstart-${pkgver}"
    autoreconf -vfi
	./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin
	make -j1 V=1 VERBOSE=1
}

package() {
	cd "upstart-${pkgver}"
	make DESTDIR="${pkgdir}/" install

    mv -v "${pkgdir}"/usr/bin/{init,upstart}
    mv -v "${pkgdir}"/usr/share/man/man5/{init,upstart}.5
    mv -v "${pkgdir}"/usr/share/man/man8/{init,upstart}.8

    # Remove files for upstart system init
    rm -v "${pkgdir}"/usr/bin/{halt,poweroff,reboot,runlevel,shutdown,telinit}
    rm -v "${pkgdir}"/usr/share/man/man5/inittab.5
    rm -v "${pkgdir}"/usr/share/man/man8/{halt,poweroff,reboot,runlevel,shutdown,telinit}.8
    rm -rv "${pkgdir}"/etc/init/

    # Install user init scripts
    install -dm755 "${pkgdir}"/usr/share/upstart/sessions/
    install -m644 debian/user-conf/*.conf \
		"${pkgdir}"/usr/share/upstart/sessions/

    # Install Ubuntu's dbus init script
    install -m644 "${srcdir}"/dbus.conf \
        "${pkgdir}"/usr/share/upstart/sessions/

    # Install Xsession script
    install -dm755 "${pkgdir}"/etc/X11/xinit/xinitrc.d/
	install -m755 "${srcdir}"/{00,99z}-upstart \
		"${pkgdir}"/etc/X11/xinit/xinitrc.d/

	# Install list of desktops in which upstart should be run
    install -dm755 "${pkgdir}"/etc/
    install -m644 "${srcdir}"/upstart-xsessions \
		"${pkgdir}"/etc/

    # Install manpages
    install -dm755 "${pkgdir}"/usr/share/man/man7/
    install -m644 debian/manpages/upstart-events.7 \
        "${pkgdir}"/usr/share/man/man7/

    # Install bash-completion script
    install -dm755 "${pkgdir}"/usr/share/bash-completion/completions/
    install -m644 contrib/bash_completion/upstart \
		"${pkgdir}"/usr/share/bash-completion/completions/
}
