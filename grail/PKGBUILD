# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: thn81 <root@scrat>

pkgname=grail
_actual_ver=3.1.0
_extra_ver=daily13.02.26
pkgver=${_actual_ver}.${_extra_ver}
pkgrel=105
pkgdesc="Gesture Recognition And Instantiation Library"
arch=('i686' 'x86_64')
url="https://launchpad.net/grail"
license=('LGPL')
depends=('frame' 'libxfixes' 'libxi')
makedepends=('inputproto')
checkdepends=('xorg-gtest')
options=('!libtool')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/grail_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('72572dc7b75957127ddadc8ee1e0509098a3ea11a2e20349d2b77f97cdea920dc34f86261d501fd5c6efcc64c20bf5e6ecc57c41ad76c5c79934f372060aede6')

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi

  # Static library needed for tests
  ./configure --prefix=/usr --with-x11 --disable-static
  make -j1
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  # Tests currently fail on Ubuntu as well
  make -j1 check || :
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
