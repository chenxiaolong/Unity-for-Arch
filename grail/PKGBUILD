# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety
# vercheck-launchpad: name=${pkgname}

pkgname=grail
_actual_ver=3.1.0
_extra_ver=+16.04.20160125
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
epoch=1
pkgdesc="Gesture Recognition And Instantiation Library"
arch=(i686 x86_64)
url="https://launchpad.net/grail"
license=(LGPL)
depends=(frame libxfixes libxi)
makedepends=(inputproto)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/grail_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('b54f6681e128a1cf79e1fa74452d4aa674a3eaa5ce3d46386760382c5b754a582022a43b516149fa4f7fc68397c6d80b852550c9ed04e9bdae06ae0713a9de4d')

build() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    autoreconf -vfi

    # Static library needed for tests
    ./configure --prefix=/usr --with-x11 --disable-static
    make -j1
}

package() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"
    make DESTDIR="${pkgdir}/" install
}
