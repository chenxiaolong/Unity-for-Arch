# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Jan de Groot <jgc@archlinux.org>

# This package is modified from the Arch Linux xorg-server package.
# Changes:
#  - Only contains the xorg-server and xorg-server-devel packages
#  - Adds 500_pointer_barrier_thresholds.diff patch from Ubuntu
#    - Manually refreshed for Xorg Server 1.12.0
#    - list                -> xorg_list
#    - list_add            -> xorg_list_add
#    - list_del            -> xorg_list_del
#    - list_for_each_entry -> xorg_list_for_each_entry
#    - swaps(param1, param2) -> swaps(param1)
#    - swapl(param1, param2) -> swapl(param1)

pkgbase=xorg-server
pkgname=('xorg-server-ubuntu' 'xorg-server-devel-ubuntu')
pkgver=1.12.3.901
pkgrel=100
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org"
makedepends=('pixman' 'libx11' 'mesa' 'libgl' 'xf86driproto' 'xcmiscproto' 'xtrans' 'bigreqsproto' 'randrproto' 'inputproto' 'fontsproto' 'videoproto' 'compositeproto' 'recordproto' 'scrnsaverproto' 'resourceproto' 'xineramaproto' 'libxkbfile' 'libxfont' 'renderproto' 'libpciaccess' 'libxv' 'xf86dgaproto' 'libxmu' 'libxrender' 'libxi' 'dmxproto' 'libxaw' 'libdmx' 'libxtst' 'libxres' 'xorg-xkbcomp' 'xorg-util-macros' 'xorg-font-util' 'glproto' 'dri2proto' 'udev' 'libgcrypt')
options=('!libtool')
source=("${url}/releases/individual/xserver/${pkgbase}-${pkgver}.tar.bz2"
        'autoconfig-nvidia.patch'
        'autoconfig-sis.patch'
        '10-quirks.conf'
        '500_pointer_barrier_thresholds_refreshed_for_1.12.3.diff')
sha512sums=('99de5f87c224eec84520a525ece51f856b7c7eaca2964f981e4c854f30110170aca80e938206d4f3779a3595f5efeef2ed3432d7d007957181b4bd4763de5cd0'
            '8e0dc38e28930e1a2682d25608d62d5e8cae9360c677d44760840b2424bf00d30ee434cb141b090dc37da1458fe619011baf375b32f250bd2b46ca536ac92de6'
            '3211dbc6d8a84f3f6a77230a8e5fe13355b441768d864c2b3037b893be39f7b8d581c2a5db1e04f399ce19c4ced973b64eaad563d970ee9c7b8a93cb23a0aa18'
            '9a1a5568be751435daea720cfc4bad209d62545cc10ea2f49113c41669c8130809a680492256ef331757fe8539d2e0e5e9e67a36f7c48c8d92d9b3e957d28fa2'
            '04cfb17de231753fe124886a1d80c864a94de9deb952189c5d6ebaa3f15bf8d679c3f94c7af289de6b4e81b9a8ec72998c19f225e75ccbafbde98b6fa5759ec2')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  # Use nouveau/nv/nvidia drivers for nvidia devices
  patch -Np1 -i "${srcdir}/autoconfig-nvidia.patch"

  # Use unofficial imedia SiS driver for supported SiS devices
  patch -Np0 -i "${srcdir}/autoconfig-sis.patch"

  # Patch from Ubuntu for XFixes protocol version 6
  # (needed for revealing the launcher in Unity)
  # Refreshed for 1.12.0 by Xiao-Long Chen
  # Refreshed for 1.12.0.901 by ecopunk (https://github.com/ecopunk)
  patch -Np1 -i "${srcdir}/500_pointer_barrier_thresholds_refreshed_for_1.12.3.diff"

  autoreconf -vfi

  ./configure \
      --prefix=/usr \
      --enable-ipv6 \
      --enable-dri \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-kdrive-evdev \
      --enable-kdrive-kbd \
      --enable-kdrive-mouse \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-fontrootdir=/usr/share/fonts

  make ${MAKEFLAGS}

  # Disable subdirs for make install rule to make splitting easier
  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -i hw/Makefile
}

package_xorg-server-ubuntu() {
  pkgdesc="Xorg X server"
  depends=('libdrm' 'pixman' 'libgcrypt' 'xorg-server-common' 'xf86-input-evdev')
  backup=('etc/X11/xorg.conf.d/10-evdev.conf' 'etc/X11/xorg.conf.d/10-quirks.conf')
  provides=('x-server' "xorg-server=${pkgver}")
  conflicts=('nvidia-utils<=290.10' 'xorg-server')
  groups=('xorg')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/etc/X11"
  mv "${pkgdir}/usr/share/X11/xorg.conf.d" "${pkgdir}/etc/X11/"
  install -m644 "${srcdir}/10-quirks.conf" "${pkgdir}/etc/X11/xorg.conf.d/"

  rmdir -v "${pkgdir}/usr/share/X11"

  # Needed for non-mesa drivers, libgl will restore it
  mv -v "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" \
        "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.xorg"

  rm -rvf "${pkgdir}/var"

  rm -vf "${pkgdir}/usr/share/man/man1/Xserver.1"
  rm -vf "${pkgdir}/usr/lib/xorg/protocol.txt"

  install -dm755 "${pkgdir}/usr/share/licenses/xorg-server"
  ln -svf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server/COPYING"

  rm -rvf "${pkgdir}/usr/lib/pkgconfig"
  rm -rvf "${pkgdir}/usr/include"
  rm -rvf "${pkgdir}/usr/share/aclocal"
}

package_xorg-server-devel-ubuntu() {
  arch=('any')
  pkgdesc="Development files for the X.Org X server"
  depends=('xproto' 'randrproto' 'renderproto' 'xextproto' 'inputproto' 'kbproto' 'fontsproto' 'videoproto' 'dri2proto' 'xineramaproto' 'xorg-util-macros' 'pixman' 'libpciaccess')
  provides=("xorg-server-devel=${pkgver}")
  conflicts=('xorg-server-devel')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rvf "${pkgdir}/usr/bin"
  rm -rvf "${pkgdir}/usr/share/man"
  rm -rvf "${pkgdir}/usr/share/doc"
  rm -rvf "${pkgdir}/usr/share/X11"
  rm -rvf "${pkgdir}/usr/lib/xorg"
  rm -rvf "${pkgdir}/var"

  install -dm755 "${pkgdir}/usr/share/licenses/xorg-server-devel"
  ln -svf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-devel/COPYING"
}

# vim:set ts=2 sw=2 et:
