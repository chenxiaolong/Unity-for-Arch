# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Based on Arch's gnome-control-center package

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=vivid

pkgname=unity-control-center
_actual_ver=15.04.0
_extra_ver=+15.04.20141030
pkgver=${_actual_ver}${_extra_ver/+/.}
pkgrel=1
pkgdesc="The Control Center for Unity"
arch=(i686 x86_64)
groups=(unity)
url="https://launchpad.net/unity-control-center"
license=(GPL)
depends=(accountsservice cups-pk-helper libnotify gnome-menus
         gsettings-desktop-schemas gtk3-ubuntu libgtop libtimezonemap
         network-manager-applet sound-theme-freedesktop upower
         libpwquality gnome-color-manager smbclient libmm-glib libgnomekbd
         grilo clutter-gtk unity-settings-daemon system-config-printer
         webkitgtk)
makedepends=(gnome-common gnome-doc-utils intltool docbook-xsl modemmanager
             ttf-ubuntu-font-family vala)
optdepends=('gnome-control-center: For bluetooth panel')
conflicts=(upower-compat)
options=(!emptydirs)
install=unity-control-center.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-control-center_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Revert-54_enable_alt_tap_in_shortcut.patch.patch
        0002-Skip-bluetooth-panel.patch
        0003-Skip-region-panel.patch
        0004-Remove-Ubuntu-specific-language-code.patch
        0005-Remove-Ubuntu-help-links.patch
        0006-Allow-gnome-control-center-to-run-at-the-same-time.patch
        0008-Remove-overlay-scrollbar-from-GTK_MODULES.patch
        system-config-printer-unity.desktop
        unity-bluetooth-panel.desktop
        unity-online-accounts-panel.desktop
        unity-region-panel.desktop)
sha512sums=('c6c63219247687246cb6f5a4e03d7ee92b3107d63af475dfdb0ca026a4589e4cc28d1fd5563c8f52b20e15fb36ed0157e8dac8b4a27a24a4f06cf0b54898d7a8'
            '1b39906307c6776647cb1325c4029806614f5301e0c7fd1fd6c14509daad587ddbf71f5ac60c81e7f4c89ed9598569523a31ec3f07ddc69e8cd5b73e205ec700'
            'ba07ad32de0ebb52e578e53506e7bff3826c6c15dbd415429f60ffa3c2de0884a7fc5866d9e53a064fdd5631d12dbcd925336ae994259cd31f77d5f3f827fc6b'
            '17a8ec0ea635dcc991c6c101a0865c09d6aff344455020272a189af00579451c7fa9348063c30b80cd74b8e624d466a04862915dd0b343d9395e6c2f4e80085e'
            '0eb193899552ae5bfd2d16817993ba1f0db78e9e3cdf141f72766c3a17fc9f73cea1893fe969687c4197cd23fcb93805226d0193cdcb092d02137d93df14b031'
            '57d517491855ffb8db9e872bc99d7eedffdbc77eb864bb7e6dd8ba6ece58565823a93b0ae498b3b5e03f6bda019c96e68866452e6fc5957951a08b032999e4a4'
            '0743d3f89473751557bda5c2a6957c27f36b11b757ec6128d5603f48f6d4626d8097ea0e4e0ebf87d27830cea6364178e471e84b5ed853705dad11be240dc01c'
            'c5da31eb5a4e00420f815b5ce11f12fb27f52d057f75514495d6b7dbec0ffd05ad12937273192e67edbf5728edf0a3637856ac24678c55ef6951dd0e93c4eaf4'
            '863e47afa95c8b16c8824c311c2b9d97b29f53153ee85a12989c63bbee4abb2e2626ef2d85988ac9d279baa60edcb32b65582acf4541f56fbc7efc22e2565de0'
            '664ddfca6cc0526cf6a058b9cfa0d163d708b397166e9bade15806a43f6175b98430d41f8ef4ce3d33dc571d79013e5b7340df9aeecd2dc37ec89248d2c6fa5a'
            '287bf010b9a6d9424e2f07f7271cd27fd9a0fb9e29ea704dbb211a67d23558c66c0c654ee2a23456cba69bee725e962fbe79713cc0d2ad0ba4d830b0053777e7'
            '1378c3c15cb8c60575a55c86d400878f021c35adc87618c219496fee086b3c1286d7a46051747312960806330b6bb911a0c3e33814acedb2d83a1ef57d5e7a3a')

prepare() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"
  patch -p1 -i ../0001-Revert-54_enable_alt_tap_in_shortcut.patch.patch
  patch -p1 -i ../0002-Skip-bluetooth-panel.patch
  patch -p1 -i ../0003-Skip-region-panel.patch
  patch -p1 -i ../0004-Remove-Ubuntu-specific-language-code.patch
  patch -p1 -i ../0005-Remove-Ubuntu-help-links.patch
  patch -p1 -i ../0006-Allow-gnome-control-center-to-run-at-the-same-time.patch
  patch -p1 -i ../0008-Remove-overlay-scrollbar-from-GTK_MODULES.patch
}

build() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/${pkgname} \
    --disable-static \
    --enable-systemd

  # https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd "${pkgname}-${_actual_ver}${_extra_ver}"

  make DESTDIR="${pkgdir}" install

  # Fix a warning
#   chown 102:0 "${pkgdir}/usr/share/polkit-1/rules.d/"
  chmod 700 "${pkgdir}/usr/share/polkit-1/rules.d/"

  # Use language packs
  rm -r "${pkgdir}/usr/share/locale/"

  panels/info/logo-generator \
    --logo panels/info/UbuntuLogoBlank.png \
    --text "Unity for Arch" \
    --output "${pkgdir}/usr/share/unity-control-center/ui/UbuntuLogo.png"

  # We'll just ship another desktop file for system-config-printer instead of
  # providing a patched package
  install -m644 "${srcdir}/system-config-printer-unity.desktop" \
                "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/unity-bluetooth-panel.desktop" \
                "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/unity-online-accounts-panel.desktop" \
                "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/unity-region-panel.desktop" \
                "${pkgdir}/usr/share/applications/"
}
