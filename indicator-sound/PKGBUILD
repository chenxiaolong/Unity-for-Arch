# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Balló György <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=zesty
# vercheck-launchpad: name=${pkgname}

pkgname=indicator-sound
_actual_ver=12.10.2
_extra_ver=+17.04.20161021
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
epoch=1
pkgdesc="A unified sound menu"
arch=(i686 x86_64)
url="https://launchpad.net/indicator-sound"
license=(GPL)
depends=(accountsservice dconf gsettings-ubuntu-schemas libgee libnotify
         libpulse libxml2 pulseaudio qt5-base unity-api unity-control-center)
makedepends=(cmake gobject-introspection intltool vala)
groups=(unity)
options=(!emptydirs)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-sound_${_actual_ver}${_extra_ver}.orig.tar.gz"
        0001-Not-running-on-Ubuntu-Touch.patch
        0002-Remove-tests.patch)
sha512sums=('c6e0c3854af9044fe4cfa2af9a67890b5803e9d55996a47d27334c1da4622a2adb11ba257cdca9f1525f476c19fd64f21f01cebd87118dc4866616da78171c99'
            '3c57829cd55b1aea14b5bc2f66e8b015d28ccdd8a7d56c5d445efb61d149a49159e6be0c267d8eded5884d53361705373837cfe789d39ced9d262a5fa630491f'
            'a2c8b9f23419c8554a38ac6e3128841e38d15e6ca2f1c70a28a01546ca61090059304974a2d5d8ed5e7c9cc68a2762c6b856d575bae3d74e81b45c0b13e517ce')

prepare() {
    patch -p1 -i ../0001-Not-running-on-Ubuntu-Touch.patch
    patch -p1 -i ../0002-Remove-tests.patch
}

build() {
    [[ -d build ]] || mkdir build
    pushd build
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib
    make
    popd
}

package() {
    cd build

    make DESTDIR="${pkgdir}/" install

    rm -v "${pkgdir}"/usr/var/lib/polkit-1/localauthority/10-vendor.d/50-com.canonical.indicator.sound.AccountsService.pkla
}
