# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Balló György <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

pkgname=indicator-sound
_actual_ver=12.10.2
_extra_ver=daily13.05.02
_translations=20130418
pkgver=${_actual_ver}${_extra_ver}
pkgrel=100
pkgdesc="A unified sound menu"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-sound"
license=('GPL')
depends=('libbamf3' 'libpulse' 'libindicator3' 'libdbusmenu-gtk3' 'ido' 'libnotify' 'libgee' 'dconf')
makedepends=('intltool' 'vala' 'libgee')
groups=('unity')
options=('!libtool')
install=${pkgname}.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/indicator-sound_${_actual_ver}${_extra_ver}.orig.tar.gz"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-indicator-sound.tar.gz")
sha512sums=('97567d9b178ddcc5195f05864939f7d98b80fbf6928df5992b99e5c3da1c2942a804d7900bb8d64f6054d7f8db42d5e270eeff4dd8ece2778740037dfd96c181'
            '88578aab046cc6e87e015e414b21cf06ab51d26ee932e4213c4e532fd70f373fb03257c5d7b2bbcddbc852816dd1206b934ff9dc4d65dbac3325b382553230b0')

prepare() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done
}

build() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}"

  autoreconf -vfi
  intltoolize -f

  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} --disable-static --disable-schemas-compile
  make -j1
}

check() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/"

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_actual_ver}${_extra_ver}/"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
