# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=yakkety

pkgname=unity-api
_actual_ver=7.119
_extra_ver=+16.10.20160909
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="API for Unity shell integration"
arch=(i686 x86_64)
url="https://launchpad.net/unity-api"
license=(LGPL)
groups=(unity)
depends=(glib2)
makedepends=(cmake doxygen graphviz python qt5-base)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('110d1681f2a88fdf126424d87597ae55f4792fa6fa94fee0be61a0904ca5d365d30850f6cba25b37500d5475c572edca4f09007482e0275b8f63ca2891cd4483')

build() {
    mkdir -p build
    cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DNO_TESTS=TRUE
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}/" install
}
