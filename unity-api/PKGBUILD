# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=zesty

pkgname=unity-api
_actual_ver=8.2
_extra_ver=+17.04.20170206
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
pkgdesc="API for Unity shell integration"
arch=(i686 x86_64)
url="https://launchpad.net/unity-api"
license=(LGPL)
groups=(unity)
depends=(glib2)
makedepends=(cmake doxygen graphviz python qt5-base)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${_actual_ver}${_extra_ver}.orig.tar.gz")
sha512sums=('897a003a47a286a2c226c216b21c9b1b73603be5520a2ffa7ff0e5f76403bb765a56f6e2202fccd3a5d71a14bfbaa9930bcee92483bd37e43f0bff101004cc1e')

build() {
    mkdir -p build
    cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DNO_TESTS=TRUE
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}/" install
}
