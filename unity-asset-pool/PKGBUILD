# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: thn81 <root@scrat>

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname}, repo=xenial
# vercheck-launchpad: name=${pkgname}

pkgname=unity-asset-pool
_actual_ver=0.8.24
_extra_ver=+15.04.20141217
pkgver=${_actual_ver}${_extra_ver/\+/.}
pkgrel=1
epoch=1
pkgdesc="Design assets for Unity"
arch=(any)
url="https://launchpad.net/unity-asset-pool"
license=(GPL)
depends=(gnome-icon-theme)
makedepends=(icon-naming-utils)
groups=(unity)
source=("https://launchpad.net/ubuntu/+archive/primary/+files/unity-asset-pool_${_actual_ver}${_extra_ver}.orig.tar.gz"
        archlinux-icon-tango-22.png
        archlinux-icon-tango-24.png)
sha512sums=('cd7b55b896b71c62f62e43886fc32294ac444b821bf255f725a599dd13558829d8dcb4f0e51c581ae2e232804f1d9a6aa629eb9f015abe313d1d6986bfc23cf2'
            '39d4bdec83581562c17e232b29af92596cd838509983581f5637dc7534e51330c794918df4190dd54b4b5b641a5687aef914f68159ea9ec2ca4e377f707eb681'
            '37021e5f93371cb78ea71d150dee840cb76f4590de79b057a6a72a60d88168b09c2ef7fe96ec552cfe977b3a416437e700efc82876aaefb75c4b4745659e0993')

package() {
    cd "${pkgname}-${_actual_ver}${_extra_ver}"

    install -dm755 "${pkgdir}/usr/share/unity/themes/"
    install -m644 launcher/* "${pkgdir}/usr/share/unity/themes/"
    install -m644 panel/* "${pkgdir}/usr/share/unity/themes/"

    install -dm755 "${pkgdir}/usr/share/icons/"
    find unity-icon-theme/ -type f -exec install -Dm644 {} "${pkgdir}"/usr/share/icons/{} \;
    install -m644 "${srcdir}/archlinux-icon-tango-22.png" "${pkgdir}/usr/share/icons/unity-icon-theme/places/22/distributor-logo.png"
    install -m644 "${srcdir}/archlinux-icon-tango-24.png" "${pkgdir}/usr/share/icons/unity-icon-theme/places/24/distributor-logo.png"

    # From debian/rules
    for i in $(find "${pkgdir}/usr/share/icons" -mindepth 2 -maxdepth 2 -type d); do
        cd "${i}"
        for j in *; do
            /usr/lib/icon-naming-utils/icon-name-mapping -c "${j}"
        done
    done
}
